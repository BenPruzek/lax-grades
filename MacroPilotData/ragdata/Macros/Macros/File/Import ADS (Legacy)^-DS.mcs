' Import ADS Model
' !!! Do not change the line above !!!

' ================================================================================================
' Copyright 2009-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
' ------------------------------------------------------------------------------------------------
' 24-Jul-2009 ube: GetMacroPath replaced by GetInstallPath + "\Library\Macros" (pervisouly only first macropath was search)
' ------------------------------------------------------------------------------------------------

Sub Main ()
    
Dim sProjectName As String
Dim sSimplifyAngle As String
Dim sSimplifyAdjacentTol As String
Dim sSimplifyRadiusTol As String
Dim sSimplifyMinPointsArc As String
Dim sSimplifyMinPointsCircle As String
Dim sSimplifyAngleTang As String
Dim sSimplifyEdgeLength As String
Dim sMinimumMetalThickness As String
Dim bUseSheets As Boolean
Dim bUseSimplification As Boolean
Dim bCopyFileToProject As Boolean
Dim bUseWaveguidePorts As Boolean
    
BeginHide

	sProjectName = GetFilePath("proj_a", "*", "", "Select ADS 3D EM Project To Open")


	If (sProjectName <> "") Then

		Dim cst_result As Integer
  
	Begin Dialog UserDialog 275,430,"ADS Import Options",.DialogFunc ' %GRID:5,5,1,1
		Text 35,265,180,15,"Radius tolerance (percent):",.Text1
		GroupBox 10,95,255,300,"Simplification",.GroupBox2
		CheckBox 20,160,205,15,"Use simplification",.UseSimplification
		GroupBox 20,205,235,105,"Circle/arc conversion",.GroupBox1
		CheckBox 20,180,210,15,"Use zero metal thickness",.UseSheets
		TextBox 35,280,195,20,.SimplifyRadiusTol
		Text 35,225,200,15,"Arc resolution (degrees):",.Text2
		TextBox 35,240,195,20,.SimplifyAngle
		GroupBox 20,320,235,65,"Small edge removal",.GroupBox3
		Text 35,340,140,15,"Minimum edge length:",.Text3
		Text 20,115,190,15,"Minimum metal thickness:",.Text4
		TextBox 35,355,195,20,.SimplifyEdgeLength
		TextBox 20,130,195,20,.MinimumMetalThickness
		OKButton 20,405,100,20
		CancelButton 10,30,0,0
		PushButton 145,405,100,20,"Help",.Help
		CheckBox 10,10,235,15,"Copy file to project",.CopyFileToProject
		GroupBox 10,35,255,55,"Port settings",.GroupBox4
		OptionGroup .Discrete_Or_Waveguide
			OptionButton 20,50,235,20,"Use discrete ports only",.UseDiscretePorts
			OptionButton 20,70,240,15,"Use waveguide ports on boundary",.UseWaveguidePorts
	End Dialog
		Dim dlg As UserDialog

		dlg.SimplifyAngle      = "5.0"
		dlg.SimplifyRadiusTol  = "5.0"
		dlg.SimplifyEdgeLength = "0.0"
		dlg.MinimumMetalThickness = "0.0"
		dlg.UseSimplification  = 1
		dlg.UseSheets = 0
		dlg.Discrete_Or_Waveguide = 0
		dlg.CopyFileToProject = 0
		cst_result = Dialog(dlg)

		sSimplifyAngle           = dlg.SimplifyAngle
		sSimplifyAdjacentTol     = "1.0"
		sSimplifyRadiusTol       = dlg.SimplifyRadiusTol
		sSimplifyMinPointsArc    = "3"
		sSimplifyMinPointsCircle = "5"
		sSimplifyAngleTang       = "1.0"
		sSimplifyEdgeLength      = dlg.SimplifyEdgeLength
		bUseSimplification       = dlg.UseSimplification
		If dlg.Discrete_Or_Waveguide = 0 Then
			bUseWaveguidePorts   = False
		Else
			bUseWaveguidePorts   = True
		End If

		bCopyFileToProject 		 = dlg.CopyFileToProject

		sMinimumMetalThickness = dlg.MinimumMetalThickness
		bUseSheets             = dlg.UseSheets

		If bCopyFileToProject Then

			Dim sProjectPath3D As String
			sProjectPath3D = getprojectpath("Project") + "\Model\3D\"

			Dim sADSPath As String
			Dim sADSProject As String
			Dim nIndex As Integer
			nIndex = InStrRev(sProjectName, "\")
			If (nIndex = 0) Then
				GoTo Skip
			End If

			sADSPath = Trim(Left(sProjectName, nIndex))
			sADSProject = Trim(Mid(sProjectName, nIndex+1))

			CopyFileToProject(sADSPath, sProjectPath3D, "proj.ltd")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj.pin")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj.prt")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj.sub")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj.lmp")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj.sti")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj")
			CopyFileToProject(sADSPath, sProjectPath3D, "proj_a")
			CopyFileToProject(sADSPath, sProjectPath3D, "emStateFile.xml")

			sProjectName = "*" + sADSProject

		End If

		Assign "sProjectName"    ' variable will be written into the history.
		Assign "sSimplifyAngle"        ' variable will be written into the history.
		Assign "sSimplifyAdjacentTol"
		Assign "sSimplifyRadiusTol"
		Assign "sSimplifyMinPointsArc"
		Assign "sSimplifyMinPointsCircle"
		Assign "sSimplifyAngleTang"
		Assign "sSimplifyEdgeLength"
		Assign "sMinimumMetalThickness"
		Assign "bUseSheets"
		Assign "bUseSimplification"
		Assign "bUseWaveguidePorts"

		Skip:

	End If

EndHide
    
If (sProjectName <> "") Then
    
    StoreGlobalDataValue "Macros\ADS Import\FileName", sProjectName
    StoreGlobalDataValue "Macros\ADS Import\SimplifyAngle", sSimplifyAngle
    StoreGlobalDataValue "Macros\ADS Import\SimplifyAdjacentTol", sSimplifyAdjacentTol
    StoreGlobalDataValue "Macros\ADS Import\SimplifyRadiusTol", sSimplifyRadiusTol
    StoreGlobalDataValue "Macros\ADS Import\SimplifyMinPointsArc", sSimplifyMinPointsArc
    StoreGlobalDataValue "Macros\ADS Import\SimplifyMinPointsCircle", sSimplifyMinPointsCircle
    StoreGlobalDataValue "Macros\ADS Import\SimplifyAngleTang", sSimplifyAngleTang
    StoreGlobalDataValue "Macros\ADS Import\SimplifyEdgeLength", sSimplifyEdgeLength
    StoreGlobalDataValue "Macros\ADS Import\MinimumMetalThickness", sMinimumMetalThickness
    StoreGlobalDataValue "Macros\ADS Import\UseSheets", bUseSheets
    StoreGlobalDataValue "Macros\ADS Import\UseSimplification", bUseSimplification
    StoreGlobalDataValue "Macros\ADS Import\UseWaveguidePorts", bUseWaveguidePorts

    RunScript GetInstallPath + "\Library\Macros\File\ADS Converter 1.bas"
    
End If

End Sub

Function CopyFileToProject(sADSPath As String, sProjectPath3D As String, sFileName As String)

	If DoesFileExist(sADSPath + sFileName) Then
		FileCopy(sADSPath + sFileName, sProjectPath3D + sFileName)
	End If

End Function

Function DoesFileExist(sFileName As String)

  Dim bCouldBeOpened As Boolean
  bCouldBeOpened = False

  On Error GoTo Finish

  Open sFileName For Input As #1
  Close #1
  bCouldBeOpened = True

  Finish:

  DoesFileExist = bCouldBeOpened

End Function

Function DialogFunc%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
	Case 1 ' Dialog box initialization
	Case 2 ' Value changing or button pressed
		Select Case Item
		Case "Help"
			StartHelp "common_macro_ADSSimplificationSettings"
			DialogFunc = True
		End Select
	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
