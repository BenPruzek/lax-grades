' Construct / Superconducting Cavity
' !!! Do not change the line above !!!
'
' macro.520
' ================================================================================================
' Copyright 2004-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'------------------------------------------------------------------------------------------
' 27-Aug-2009 kko: added points on the linear segment of the spline to have a rather uniform spacing for the spline
' 21-Aug-2009 ube: prevent setting spline-points twice or too close (variable dMinPointDistance introduced)
' 24-Jul-2009 ube: GetMacroPath replaced by GetInstallPath + "\Library\Macros" (pervisouly only first macropath was search)
' 23-Jul-2007 jsw: alow rx1+rx2>xlen2
' 11-Oct-2005 ube: Included into Online Help
' 13-Jul-2004 ube: first version
'------------------------------------------------------------------------------------------

Option Explicit

Sub Main ()

Dim sr1 As String, srx1 As String, sry1 As String
Dim sr2 As String, srx2 As String, sry2 As String, sxlen2 As String

Dim r1 As Double, rx1 As Double, ry1 As Double
Dim r2 As Double, rx2 As Double, ry2 As Double, xlen2 As Double

Dim cst_result       As Integer

BeginHide
	Dim bRedefine As Boolean
	Begin Dialog UserDialog 435,335,"Superconducting cavity (half cell)",.DialogFunc ' %GRID:5,5,1,1
		Picture 15,5,410,195,GetInstallPath + "\Library\Macros\Construct\Superconducting Cavity_tesla.bmp",0,.Picture1
		OKButton 20,310,90,20
		CancelButton 120,310,90,20
		GroupBox 10,210,420,90,"",.GroupBox1
		Text 20,230,20,15,"r1",.Text1
		TextBox 55,225,90,20,.r1
		Text 160,230,40,15,"xlen2",.Text2
		TextBox 200,225,90,20,.xlen2
		Text 300,230,20,15,"r2",.Text3
		TextBox 330,225,90,20,.r2
		Text 20,255,30,15,"rx1",.Text4
		Text 20,280,30,15,"ry1",.Text5
		TextBox 55,250,90,20,.rx1
		TextBox 55,275,90,20,.ry1
		Text 300,255,30,15,"rx2",.Text6
		Text 300,280,30,15,"ry2",.Text7
		TextBox 330,250,90,20,.rx2
		TextBox 330,275,90,20,.ry2
		PushButton 220,310,90,20,"Help",.Help
	End Dialog
	Dim dlg As UserDialog

	dlg.r1  = "35"
	dlg.rx1 = "12"
	dlg.ry1 = "19"

	dlg.xlen2 = "57.692"

	dlg.r2  = "103.3"
	dlg.rx2 = "42"
	dlg.ry2 = "42"


        cst_result = Dialog(dlg)
        assign "cst_result"        '  writes e.g. "cst_result = -1/0/1"     into history list
        If (cst_result = 0) Then Exit All
        'If (cst_result = 1) Then
		'	StartHelp "common_preloadedmacro_construct_superconducting_cavity"
        'End If

		sr1  = dlg.r1
		srx1 = dlg.rx1
		sry1 = dlg.ry1

		sxlen2 = dlg.xlen2

		sr2  = dlg.r2
		srx2 = dlg.rx2
		sry2 = dlg.ry2

		r1  = Evaluate(sr1)
		rx1 = Evaluate(srx1)
		ry1 = Evaluate(sry1)

		xlen2 = Evaluate(sxlen2)

		r2  = Evaluate(sr2)
		rx2 = Evaluate(srx2)
		ry2 = Evaluate(sry2)

	'	bRedefine = False

	'	If ( r1<=0 Or rx1<=0 Or ry1<=0 Or _
	'		 r2<=0 Or rx2<=0 Or ry2<=0 Or xlen2 <=0	) Then
	'		MsgBox "Unsupported values:" + vbCrLf + _
	'			"all values have to be defined as postivie numbers" + vbCrLf + _
	'			"Please redefine.", vbExclamation
	'		bRedefine = True
	'	Else
	'		If ( rx1+rx2 >= xlen2 ) Then
	'			MsgBox "Unsupported values:" + vbCrLf + _
	'				"rx1+rx2 has to be smaller than xlen2" + vbCrLf + _
	'				"Please redefine.", vbExclamation
	'			bRedefine = True
	'		Else
	'			If ( r1+ry1 > r2-ry2 ) Then
	'				MsgBox "Unsupported values:" + vbCrLf + _
	'					"ellipse1 center y-coordinate has to be smaller than ellipse2 center y-coordinate " + vbCrLf + _
	'					"Please redefine.", vbExclamation
	'				bRedefine = True
	'			End If
	'		End If
	'	End If
	'Loop Until  (Not bRedefine)

	assign "sr1"
	assign "srx1"
	assign "sry1"

	assign "sr2"
	assign "srx2"
	assign "sry2"

	assign "sxlen2"

EndHide

cst_result = Evaluate(cst_result)
If (cst_result =0) Then Exit All   ' if cancel/help is clicked, exit all
If (cst_result =1) Then Exit All

r1  = Evaluate(sr1)
rx1 = Evaluate(srx1)
ry1 = Evaluate(sry1)

xlen2 = Evaluate(sxlen2)

r2  = Evaluate(sr2)
rx2 = Evaluate(srx2)
ry2 = Evaluate(sry2)

Dim cst_slop1, cst_slop2, cst_alf, cst_alf1, cst_alf2 As Double
Dim x1, x2, y1, y2 As Double
Dim d_alf, d_slop1, d_slop2 As Double
Dim cst_angle_step As Double

d_alf=0
d_slop1=0
d_slop2=0
cst_alf =0
cst_angle_step=.2 ' this is the angle step in degree (every 0.2 degree a point will be set)

'We need something similar to cst_angle_step for the linear part. We try to have rather the same distance between linear and angular created points
Dim cst_lin_step, cst_avarage_radius_help As Double
cst_avarage_radius_help = (rx1+ry1+rx2+ry2)/4.0
cst_lin_step = cst_avarage_radius_help*2*Pi/360.0*cst_angle_step

If rx1+rx2<xlen2 Then

	Do
		cst_alf =cst_alf+cst_angle_step
		cst_alf1=cst_alf
		cst_slop1=ry1/rx1*tanD(cst_alf1)
		cst_alf2=AtnD(cst_slop1*rx2/ry2)
		cst_slop2=((r2-ry2*(1-cosD(cst_alf2)))-(r1+ry1*(1-cosD(cst_alf1))))/((xlen2-rx2*sinD(cst_alf2))-rx1*sinD(cst_alf1))

		d_alf=AtnD(cst_slop2)-AtnD(cst_slop1)

	Loop Until d_alf <=0 Or cst_alf=90

ElseIf rx1+rx2=xlen2 Then

	cst_alf1=90
	cst_alf2=90

Else
	cst_alf=90

	Do
		cst_alf =cst_alf+cst_angle_step
		cst_alf1=cst_alf
		cst_slop1=ry1/rx1*tanD(cst_alf1)
		cst_alf2=AtnD(cst_slop1*rx2/ry2)+180
		cst_slop2=((r2-ry2*(1-cosD(cst_alf2)))-(r1+ry1*(1-cosD(cst_alf1))))/((xlen2-rx2*sinD(cst_alf2))-rx1*sinD(cst_alf1))

		d_alf=AtnD(cst_slop2)-AtnD(cst_slop1)

	Loop Until d_alf<=0 Or cst_alf=180

End If

'Now: calculate the length of the linear part to get the number of points in there.
Dim cst_lin_x1, cst_lin_x2, cst_lin_y1, cst_lin_y2 As Double

'Start point of the linear part
cst_lin_x1 = rx1*sinD(cst_alf1)
cst_lin_y1 = r1+ry1*(1-cosD(cst_alf1))

'End point of the linear part
cst_lin_x2 = xlen2-rx2*sinD(cst_alf2)
cst_lin_y2 = r2-ry2*(1-cosD(cst_alf2))

'calculate the length and a vector describing the linear part
Dim cst_lin_length, cst_lin_diffX, cst_lin_diffy As Double
cst_lin_diffX = cst_lin_x2-cst_lin_x1
cst_lin_diffy = cst_lin_y2-cst_lin_y1
cst_lin_length = Sqr(cst_lin_diffX*cst_lin_diffX+cst_lin_diffy*cst_lin_diffy)

'precalculating variables for "for"-loop
Dim cst_lin_NrOfSteps, cst_lin_CurrentStep As Long
Dim cst_lin_percent_done As Double
cst_lin_NrOfSteps = cst_lin_length / cst_lin_step


'@ New Curve: curve1
Curve.NewCurve "curve1"

'@ define curve spline: curve1:spline1

Dim xold, xnew, yold, ynew, dMinPointDistance As Double
xnew = 0.0
ynew = 0.0
xold = 0.0
yold = 0.0
dMinPointDistance = 5e-6

With Spline
	.Reset
	.Name "spline_tesla"
	.Curve "curve1"
	.Point "0", r1

	xold = 0.0
	yold = r1

	'First circular part
	For cst_alf=cst_angle_step To cst_alf1 STEP cst_angle_step
		xnew = rx1*sinD(cst_alf)
		ynew = r1+ry1*(1-cosD(cst_alf))
		If (xnew-xold)^2+(ynew-yold)^2 > dMinPointDistance^2 Then
			.LineTo xnew,ynew
			xold = xnew
			yold = ynew
		End If
	Next cst_alf

	'Linear part (note: we omit the start and end point on purpose!)
	'We need these points to have a not so extreme parametrization of the spline: else there would be many tiny steps for the circular parts and one huge step in between.
 For cst_lin_CurrentStep = 1 To cst_lin_NrOfSteps-1 STEP 1
		cst_lin_percent_done = cst_lin_CurrentStep / cst_lin_NrOfSteps
		xnew = cst_lin_x1 + cst_lin_percent_done*cst_lin_diffX
		ynew = cst_lin_y1 + cst_lin_percent_done*cst_lin_diffy
		If (xnew-xold)^2+(ynew-yold)^2 > dMinPointDistance^2 Then
			.LineTo xnew,ynew
			xold = xnew
			yold = ynew
		End If
	Next cst_lin_CurrentStep

	'Second circular part
	For cst_alf= cst_alf2 To cst_angle_step STEP -cst_angle_step
		xnew = xlen2-rx2*sinD(cst_alf)
		ynew = r2-ry2*(1-cosD(cst_alf))
		If (xnew-xold)^2+(ynew-yold)^2 > dMinPointDistance^2 Then
			.LineTo xnew,ynew
			xold = xnew
			yold = ynew
		End If
	Next cst_alf

	.LineTo  xlen2,r2
	.Create
End With

With Polygon
	.Reset
	.Name "polygon_tesla"
	.Curve "curve1"
	.Point 0, r1
	.LineTo 0, 0
	.LineTo xlen2, 0
	.LineTo xlen2, r2
	.Create
End With


'@ clear picks
Pick.ClearAllPicks

'@ new component: component1
Component.New "component1"

Dim snewSolid As String
snewSolid = Solid.GetNextFreeName

'@ define coverprofile: component1:solid1

With CoverCurve
     .Reset
     .Name snewSolid
     .Component "component1"
     .Material "Vacuum"
     .Curve "curve1:polygon_tesla"
     .Create
End With

'@ pick face
Pick.PickFaceFromId "component1:"+snewSolid, "1"

'@ set edge
Pick.AddEdge "0", "0", "0", "1", "0", "0"

'@ define Rotate: component1:solid2
With Rotate
	.Reset
	.Name Solid.GetNextFreeName
	.Component "component1"
	.Material "Vacuum"
	.Mode "Picks"
	.Angle "360"
	.Height "0.0"
	.RadiusRatio "1.0"
	.NSteps "0"
	.SplitClosedEdges "True"
	.SegmentedProfile "False"
	.DeleteBaseFaceSolid "True"
	.ClearPickedFace "True"
	.SimplifySolid "True"
	.UseAdvancedSegmentedRotation "True"
	.Create
End With

End Sub
Function DialogFunc%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
	Case 1 ' Dialog box initialization
	Case 2 ' Value changing or button pressed
		Select Case Item
		Case "Help"
			StartHelp "common_preloadedmacro_construct_superconducting_cavity"
			DialogFunc = True
		End Select
	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
