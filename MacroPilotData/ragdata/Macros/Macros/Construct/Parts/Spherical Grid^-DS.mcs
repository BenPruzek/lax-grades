' CreateControlGrid

Option Explicit

' ================================================================================================
' Copyright 2010-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'------------------------------------------------------------------------------------------
' 25-May-2010 mbk: first version
'------------------------------------------------------------------------------------------

Dim wert As Boolean ' global variables don't work in structure macros BUT it's anyway only used in dialog at the first run

Sub Main ()

'Dialogparameter: adius of sphere=R, inner and outer radius of grid =Ri,Ro
's=spoke Width, d=thickness in beam direction
'Number of segments in r and phi Nseg_r, Nseg_phi
Dim RK As Double, Ro As Double, Ri As Double
Dim s As Double, d As Double
Dim Nseg_r As Integer, Nseg_phi(6) As Integer

'Inner Parameters for local points of circle segments
Dim xp1 As Double, yp1 As Double, xp1b As Double, yp1b As Double
Dim xp2 As Double, yp2 As Double, xp2b As Double, yp2b As Double

'Temporary radii for circle segments
Dim ri_tmp As Double, ra_tmp As Double

Dim dr As Double, dphi_i As Double, dphi_a As Double
Dim dphi_si As Double, dphi_sa As Double

'Indices
Dim R As Integer, P As Integer

Dim cst_result As Integer

BeginHide
	Begin Dialog UserDialog 540,335,"Control Grid",.DialogFunc ' %GRID:5,5,1,1
		Picture 15,5,490,195,GetInstallPath + "\Library\Macros\Construct\Spherical Grid.bmp",0,.Picture1
		OKButton 20,310,90,20
		CancelButton 120,310,90,20
		GroupBox 10,215,485,90,"",.GroupBox1
		Text 20,230,20,15,"RK",.Text1
		TextBox 55,225,90,20,.RK
		Text 165,230,20,15,"s",.Text2
		TextBox 185,225,55,20,.s
		Text 260,230,20,15,"d",.Text3
		TextBox 285,225,45,20,.d
		Text 20,255,30,15,"Ro",.Text4
		Text 20,280,30,15,"Ri",.Text5
		TextBox 55,250,90,20,.Ro
		TextBox 55,275,90,20,.Ri
		Text 170,255,180,15,"Number of radial segments:",.Text6
		Text 170,280,190,15,"Number of azimutal segments:",.Text7
		TextBox 365,250,90,20,.Nseg_r
		PushButton 365,275,90,20,"Segments...",.DefineAzimutalSegments
'		PushButton 220,310,90,20,"Help",.Help
	End Dialog
	Dim dlg As UserDialog

	dlg.RK = "10"
	dlg.Ro = "8.8"
	dlg.Ri = "2"

	dlg.s = "0.2"
	dlg.d = "0.1"

	dlg.Nseg_r   = "3"

    cst_result = Dialog(dlg)

	assign "cst_result"

    If (cst_result = 0) Then Exit All

    StoreParameter("radius_primary_sphere", dlg.RK)
    StoreParameter("outer_radius", dlg.Ro)
    StoreParameter("inner_radius", dlg.Ri)

	StoreParameter("spoke_width",dlg.s)
    StoreParameter("thickness", dlg.d)

    StoreParameter("number_of_radial_segments", dlg.Nseg_r)

EndHide

cst_result = Evaluate(cst_result)
If (cst_result =0) Then Exit All   ' if cancel/help is clicked, exit all
If (cst_result =1) Then Exit All

RK  = Evaluate("radius_primary_sphere")
Ro  = Evaluate("outer_radius")
Ri  = Evaluate("inner_radius")

s   = Evaluate("spoke_width")
d   = Evaluate("thickness")

Nseg_r   = Evaluate("number_of_radial_segments")

'
' Next 6 lines are kind of obsolete, since the dialogfunc should ensure that the user did define the azimutal segments
'
MakeSureParameterExists("Azimutal_seg_1",6)
MakeSureParameterExists("Azimutal_seg_2",12)
MakeSureParameterExists("Azimutal_seg_3",24)
MakeSureParameterExists("Azimutal_seg_4",0)
MakeSureParameterExists("Azimutal_seg_5",0)
MakeSureParameterExists("Azimutal_seg_6",0)


Nseg_phi(0) = Evaluate("Azimutal_seg_1")
Nseg_phi(1) = Evaluate("Azimutal_seg_2")
Nseg_phi(2) = Evaluate("Azimutal_seg_3")
Nseg_phi(3) = Evaluate("Azimutal_seg_4")
Nseg_phi(4) = Evaluate("Azimutal_seg_5")
Nseg_phi(5) = Evaluate("Azimutal_seg_6")

'------------------------------------------------
' Error Handling
If(Nseg_r>6) Then
	MsgBox "Azimtal Segments can be only defined up to 6 rings. Please ask support for extension of the macro."
	Exit All
End If

Dim i As Integer
For i=0 To Nseg_r-1
	If(Nseg_phi(i)<=0) Then
		MsgBox "These settings will lead to division by 0. Please check azimutal segment settings"
		Exit All
	End If
Next
' End of Error Handling
'------------------------------------------------

'@ new component: Grid

Component.New "Grid" 


'@ define analytical face: Grid:Speheresurface

With AnalyticalFace
     .Reset 
     .Name "Speheresurface" 
     .Component "Grid" 
     .Material "PEC" 
     .LawX Cstr(RK)+ "*Sin(u/180*pi)*Cos(v/180*pi)"
     .LawY Cstr(RK)+ "*Sin(u/180*pi)*Sin(v/180*pi)"
     .LawZ Cstr(RK)+ "*Cos(u/180*pi)"
     .ParameterRangeU "0", "180" 
     .ParameterRangeV "0", "180" 
     .Create
End With


'@ activate local coordinates

WCS.ActivateWCS "local"


'@ rotate wcs

WCS.RotateWCS "u", "90.0" 


'@ move wcs

WCS.MoveWCS "local", "0.0", "0.0", -Sqr(RK^2-Ro^2)


'@ slice shape: Grid:Speheresurface

Solid.SliceShape "Speheresurface", "Grid" 


'@ delete shape: Grid:Speheresurface

Solid.Delete "Grid:Speheresurface" 


'@ new curve: curve1

Curve.NewCurve "curve1" 


'@ define curve circle: curve1:circle1

With Circle
     .Reset 
     .Name "circle1" 
     .Curve "curve1" 
     .Radius Ri
     .Xcenter "0" 
     .Ycenter "0" 
     .Segments "0" 
     .Create
End With


'@ clear picks

Pick.ClearAllPicks 


'@ define extrudeprofile: Grid:cut_0

With ExtrudeCurve
     .Reset 
     .Name "cut_0" 
     .Component "Grid" 
     .Material "Vacuum" 
     .Thickness -RK
     .Twistangle "0.0" 
     .Taperangle "0.0" 
     .Curve "curve1:circle1" 
     .Create
End With

dr		  = (Ro-Ri-(Nseg_r+1)*s)/Nseg_r


'loop over radial segments
For R=0 To Nseg_r-1

	ri_tmp = Ri +  R   *dr + (R+1)*s
	ra_tmp = Ri + (R+1)*dr + (R+1)*s

	dphi_si = s/(2*pi*ri_tmp)*360
	dphi_sa = s/(2*pi*ra_tmp)*360

	dphi_i  = 360/Nseg_phi(R) - dphi_si
	dphi_a  = 360/Nseg_phi(R) - dphi_sa

	'loop over phi segments
	For P=0 To Nseg_phi(R)-1

		xp1  = ri_tmp*CosD(P*dphi_i + P*dphi_si + dphi_si/2)
		yp1	 = ri_tmp*SinD(P*dphi_i + P*dphi_si + dphi_si/2)

		xp1b = ri_tmp*CosD((P+1)*dphi_i + P*dphi_si + dphi_si/2)
		yp1b = ri_tmp*SinD((P+1)*dphi_i + P*dphi_si + dphi_si/2)

		xp2  = ra_tmp*CosD(P*dphi_a + P*dphi_sa + dphi_sa/2)
		yp2  = ra_tmp*SinD(P*dphi_a + P*dphi_sa + dphi_sa/2)

		xp2b = ra_tmp*CosD((P+1)*dphi_a + P*dphi_sa + dphi_sa/2)
		yp2b = ra_tmp*SinD((P+1)*dphi_a + P*dphi_sa + dphi_sa/2)

		'@ define curve arc: curve1:arc1
		With Arc
	    	.Reset
    	 	.Name "arc1"
     		.Curve "curve1"
	     	.Orientation  "Clockwise"
    	 	.XCenter "0"
     		.YCenter "0"
	     	.X1 xp1b
    	 	.Y1 yp1b
     		.X2 xp1
	     	.Y2 yp1
    	 	.Angle dphi_i
     		.UseAngle "False"
		    .Segments "0"
    		.Create
		End With


		'@ define curve arc: curve1:arc2
		With Arc
    		.Reset
    	 	.Name "arc2"
		    .Curve "curve1"
    		.Orientation  "Clockwise"
     		.XCenter "0"
     		.YCenter "0"
	     	.X1 xp2b
    	 	.Y1 yp2b
     		.X2 xp2
	     	.Y2 yp2
    	 	.Angle dphi_a
     		.UseAngle "False"
	     	.Segments "0"
    	 	.Create
		End With


		'@ define curve line: curve1:line1
		With Line
    		.Reset
	     	.Name "line1"
		    .Curve "curve1"
    		.X1 xp1
	     	.Y1 yp1
    	 	.X2 xp2
     		.Y2 yp2
	     	.Create
		End With


		'@ define curve line: curve1:line2
		With Line
	    	.Reset
		    .Name "line2"
		    .Curve "curve1"
	    	.X1 xp1b
	     	.Y1 yp1b
		    .X2 xp2b
		    .Y2 yp2b
	    	.Create
		End With

	'@ define extrudeprofile: component1:solid1

	With ExtrudeCurve
    	 .Reset
	     .Name "cut_" + Cstr(R+1) + "_" + Cstr(P+1)
    	 .Component "Grid"
	     .Material "Vacuum"
    	 .Thickness RK
	     .Twistangle "0.0"
    	 .Taperangle "0.0"
	     .Curve "curve1:arc2"
    	 .Create
	End With

	Next P

Next R

'Cut away inner cylinder
Solid.Subtract "Grid:Speheresurface_1", "Grid:cut_0"

'Cut away rest of circle Segments
For R=0 To Nseg_r-1
	For P=0 To Nseg_phi(R)-1
		Solid.Subtract "Grid:Speheresurface_1", "Grid:cut_" + Cstr(R+1) + "_" + Cstr(P+1)
	Next P
Next R

If (d<>0) Then Solid.ThickenSheetAdvanced "Grid:Speheresurface_1", "Centered", d, "True"

'
' If second grid should be defined it would be great, if the WCS is starting from scratch
' such that 2 grid's nicely overlay each other
'
WCS.AlignWCSWithGlobalCoordinates
WCS.ActivateWCS "global"

End Sub
Function DialogFunc%(Item As String, Action As Integer, Value As Integer)

	' Obviously defining wert here is not a good idea, as with each call of DialogFunc
	' it's again initialized to "False" and then we run into an infinite loop

   	Select Case Action
	Case 1 ' Dialog box initialization
		wert= False
	Case 2 ' Value changing or button pressed
		Select Case Item
			Case "DefineAzimutalSegments"
				DialogFunc = True       ' Don't close the dialog box.
				wert = SpecifyAzimutalSegments()
			Case "OK"
				If (Not wert) Then
					' if azimutal segments are not yet defined user is forced to define them
					' otherwise the dialog does not close
					DialogFunc=True
					wert = SpecifyAzimutalSegments()
				Else
					'close dialog
				End If
		End Select

	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
Function SpecifyAzimutalSegments As Boolean

	Begin Dialog UserDialog 350,238,"Define Segments for each ring" ' %GRID:10,7,1,1
		Text 10,42,90,14,"Ring 1",.Text1
		Text 10,126,90,14,"Ring 4",.Text3
		Text 10,154,90,14,"Ring 5",.Text4
		Text 10,182,90,14,"Ring 6",.Text5
		Text 10,98,90,14,"Ring 3",.Text6
		Text 10,70,90,14,"Ring 2",.Text2
		OKButton 10,210,90,21
		TextBox 100,35,90,21,.Nseg_p0
		TextBox 100,63,90,21,.Nseg_p1
		TextBox 100,91,90,21,.Nseg_p2
		TextBox 100,119,90,21,.Nseg_p3
		TextBox 100,147,90,21,.Nseg_p4
		TextBox 100,175,90,21,.Nseg_p5
		Text 10,14,250,14,"Start from inside to outside",.Text7
	End Dialog
	Dim dlg As UserDialog

	dlg.Nseg_p0 =  "6"
	dlg.Nseg_p1 = "12"
	dlg.Nseg_p2 = "24"
	dlg.Nseg_p3 = "0"
	dlg.Nseg_p4 = "0"
	dlg.Nseg_p5 = "0"

	If Not(Dialog(dlg)) Then Exit All

	StoreParameter("Azimutal_seg_1",dlg.Nseg_p0)
	StoreParameter("Azimutal_seg_2",dlg.Nseg_p1)
	StoreParameter("Azimutal_seg_3",dlg.Nseg_p2)
	StoreParameter("Azimutal_seg_4",dlg.Nseg_p3)
	StoreParameter("Azimutal_seg_5",dlg.Nseg_p4)
	StoreParameter("Azimutal_seg_6",dlg.Nseg_p5)

	SpecifyAzimutalSegments = True 'Specification Done=Everything ok

End Function
