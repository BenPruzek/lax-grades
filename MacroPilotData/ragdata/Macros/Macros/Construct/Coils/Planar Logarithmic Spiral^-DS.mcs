' Construct / Coils / Logarithmic Spiral
' !!! Do not change the line above !!!
 '--------------------------------------------------------------------------------------------
'Type: Log Spiral  r1= k*exp(alpha*phi), r2= k*exp(alpha*(phi-delta))
'
'Ref:       http://www.wolfgang-rolke.de/antennas/ant_200.htm#230
'	http://www.wolfgang-rolke.de/antennas/ant_300.htm
'-----------------------------------------------------------------------------------------------------------------------------
' Copyright 2008-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'-----------------------------------------------------------------------------------------------------------------------------
' 13-Oct-2015 ube: Help button did not work
' 23-Jun-2010 ube,fhi: updated in distribution
' 21-Apr-2009 fhi: added parameters and solid name
' 07-Apr-2008 fhi: Initial version
'-------------------------------------------------
Option Explicit

Const HelpFileName = "common_preloadedmacro_Construct_Coils"

'-----------------------------------------------------------------------------------------------------------------------------

Sub Main ()

Dim cst_height As Double, cst_width As Double, cst_gap As Double, cst_inner_radius As Double, cst_turns As Double
Dim scst_height As String, scst_width As String, scst_k As String, scst_delta As String, scst_alpha As String
Dim scst_inner_radius As String, scst_turns As String, scst_increment As String, icst_spline_or_poly As Integer
Dim cst_spline_or_poly As Integer, cst_result As Integer, cst_oldcount As Integer, cst_dummyname As String
Dim cst_newname As String, cst_index As Integer, pi180 As Double, cst_index_a As Double, cst_rad As Double
Dim cst_radx As Double, cst_rady As Double, cst_increment As Double, cst_pi As Double, cst_phi_start As Double
Dim cst_progress 	As Double, cst_start_x 	As Double, cst_start_y 	As Double, cst_end_x1 	As Double
Dim cst_end_y1 	As Double, cst_end_x2 	As Double, cst_end_y2 	As Double, cst_delta As Double, cst_k As Double, cst_alpha As Double
Dim cname As String, dcst_height As Double, dcst_alpha As Double, dcst_delta  As Double, dcst_inner_radius As Double
Dim first As String,  seconditem As String, dcst_turns As Double, dcst_increment As Double

pi180= Atn(1)*4/180. 	' = pi/180
cst_pi = Atn(1)*4.		' = PI

BeginHide

	Begin Dialog UserDialog 440,312,"Angle-constant log. Spiral",.DialogFunc ' %GRID:10,3,1,1
		GroupBox 10,0,420,280,"",.GroupBox1
		Text 20,7,100,14,"Thickness",.HeightLabel
		TextBox 20,24,100,21,.Height
		Text 20,96,110,15,"Initial Radius (=k)",.InnerRadiusLabel
		TextBox 20,114,100,21,.InnerRadius
		Text 20,49,110,14,"Number of Turns",.LabelTurns
		TextBox 20,66,100,21,.Turns
		GroupBox 130,21,120,63,"",.GroupBox2
		OptionGroup .Spline_or_Polygon
			OptionButton 150,35,70,21,"Spline",.OptionButton1
			OptionButton 150,56,80,21,"Polygon",.OptionButton2
		Text 20,189,130,14,"Phi-Increment (deg)",.Text1
		TextBox 20,210,120,21,.angle_inc
		Text 300,189,40,14,"alpha",.Text3
		TextBox 300,210,120,21,.alpha
		Text 170,189,70,14,"delta (deg)",.Text4
		TextBox 160,210,120,21,.delta
		Picture 260,21,160,105,GetInstallPath + "\Library\Macros\Construct\Coils\Log_spiral.bmp",0,.Picture1
		Text 20,238,130,14,"r1= k*exp(alpha*phi) ",.Text2
		Text 220,238,170,14,"r2= k*exp(alpha*(phi-delta))",.Text5
		Text 20,140,90,14,"SolidName",.Text6
		TextBox 20,159,190,21,.sname
		OKButton 20,287,90,21
		CancelButton 120,287,90,21
		PushButton 330,288,90,21,"Help",.Help
		Text 20,259,390,14,"Note: delta defines opening angle of spiral.",.Text7
	End Dialog

        Dim dlg As UserDialog
        dlg.angle_inc   = "5"
		dlg.Height      = "1"
		dlg.alpha         = ".35" 'golden spiral
		dlg.InnerRadius = "10"
		dlg.Turns       = "3"
		dlg.delta="90"

	'find proper default-name for coils
	first=  ShortName (Resulttree.GetFirstChildName ("Components\Log_Spiral" ))
	Do While first <> ""
    	seconditem  = ShortName (Resulttree.GetNextItemName (  "Components\Log_Spiral\"+ first ))
		If seconditem <> "" Then
		 	first = seconditem
		Else
		 	Exit Do
		End If
	Loop
	If first ="" Then
		dlg.sname = "Log_coil"	'first coil's name
	Else
		dlg.sname = first+"_1"			' default for next coils
	End If


        cst_result = Dialog(dlg)
        assign "cst_result"        '  writes e.g. "cst_result = -1/0/1"     into history list
        If (cst_result = 0) Then Exit All

        cname = dlg.sname
        dcst_height       = cdbl(dlg.Height)
		dcst_delta          = cdbl(dlg.delta)
		dcst_alpha 	=  cdbl(dlg.alpha)
        dcst_inner_radius = cdbl(dlg.InnerRadius)
        dcst_turns        = cdbl(dlg.Turns)
        dcst_increment     = cdbl(dlg.angle_inc)
        icst_spline_or_poly = dlg.Spline_or_Polygon

        StoreDoubleParameter cname+"_height", dcst_height
		SetParameterDescription (cname+"_height", "Thickness of spiral")
		StoreDoubleParameter cname+"_delta", dcst_delta
		SetParameterDescription (cname+"_delta", "Phase-shift of spiral")
		StoreDoubleParameter cname+"_alpha", dcst_alpha
		SetParameterDescription (cname+"_alpha", "Progression of spiral")
		StoreDoubleParameter cname+"_inner_r", dcst_inner_radius
		SetParameterDescription (cname+"_inner_r", "Inner Radius of spiral")
		StoreDoubleParameter cname+"_turns", dcst_turns
		SetParameterDescription (cname+"_turns", "Nr of turns of spiral")
		StoreDoubleParameter cname+"_angle_inc", dcst_increment
		SetParameterDescription (cname+"_angle_inc", "Angle increment of spiral")

        assign "icst_spline_or_poly"
        assign "cname"

EndHide

If (cst_result =0) Then Exit All   ' if cancel/help is clicked, exit all
If (cst_result =1) Then Exit All

'dummy brick to activate the param. changes
    With Brick
     .Reset
     .Name "dummy_solid_log_spiral"
     .Component "Log_Spiral"
     .Material "PEC"
     .Xrange -cdbl(cname+"_angle_inc"), cname+"_angle_inc"
     .Yrange -cdbl(cname+"_turns"), cname+"_turns"
     .Zrange 0,0
     .Create
End With
Solid.Delete "Log_Spiral:dummy_solid_log_spiral"

cst_height  = restoredoubleparameter (cname+ "_height")
cst_delta  = restoredoubleparameter (cname+ "_delta")
cst_alpha  = restoredoubleparameter (cname+ "_alpha")
cst_inner_radius  = restoredoubleparameter (cname+ "_inner_r")
cst_turns  = restoredoubleparameter (cname+ "_turns")
cst_increment  = restoredoubleparameter (cname+ "_angle_inc")

cst_spline_or_poly =cint(icst_spline_or_poly)	'0 = Spline, 1= Polygon

'start modeling
cst_phi_start = 0  ' in degrees
cst_index = 0

'segmented Spiral
With Line
     .Reset
     .Name "start_line"
     .Curve "curve_spiral"
     .X1 cname+ "_inner_r"
     .Y1 "0"
	 .X2 cst_inner_radius*Exp((cst_index - cdbl(cname+ "_delta"))*pi180*cst_alpha)
     .Y2 "0"
     .Create
End With

' start-point
 cst_start_x =( cst_inner_radius)
 cst_start_y =0
 If cst_spline_or_poly = 0 Then
     Spline.Reset
     Spline.Name "archimed_a_"+CStr(cst_index)
     Spline.Curve "curve_spiral"
     Spline.Point cst_start_x, cst_start_y
 Else
	 Polygon.Reset
     Polygon.Name "archimed_a_"+CStr(cst_index)
     Polygon.Curve "curve_spiral"
     Polygon.Point cst_start_x, cst_start_y
 End If
     ' create spline-points with angle increment in degrees
     For cst_index_a = cst_increment+cst_phi_start  To 360*cst_turns STEP  cst_increment
          cst_end_x1=( cst_inner_radius)*Exp(cst_index_a*pi180*cst_alpha)*Cos(cst_index_a*pi180)
          cst_end_y1=( cst_inner_radius)*Exp(cst_index_a*pi180*cdbl(cname+ "_alpha"))*Sin(cst_index_a*pi180)
          If cst_spline_or_poly = 0 Then
           Spline.LineTo  cst_end_x1 , cst_end_y1
		  Else
			Polygon.LineTo  cst_end_x1 , cst_end_y1
		  End If
     Next
     If cst_spline_or_poly = 0 Then
      Spline.Create
     Else
      Polygon.create
     End If


 cst_index = 0
' start-point
 cst_start_x = cst_inner_radius*Exp((cst_index - cst_delta)*pi180*cst_alpha)
 cst_start_y = 0
 If cst_spline_or_poly = 0 Then
     Spline.Reset
     Spline.Name "archimed_b_"+CStr(cst_index)
     Spline.Curve "curve_spiral"
     Spline.Point cst_start_x, cst_start_y
 Else
	 Polygon.Reset
     Polygon.Name "archimed_b_"+CStr(cst_index)
     Polygon.Curve "curve_spiral"
     Polygon.Point cst_start_x, cst_start_y
 End If
     ' create spline-points with angle increment in degrees
     For cst_index_a = cst_increment+cst_phi_start  To 360*cst_turns STEP  cst_increment
          cst_end_x2=( cst_inner_radius)*Exp((cst_index_a-cst_delta)*pi180*cst_alpha)*Cos(cst_index_a*pi180)
          cst_end_y2=( cst_inner_radius)*Exp((cst_index_a-cst_delta)*pi180*cst_alpha)*Sin(cst_index_a*pi180)
          If cst_spline_or_poly = 0 Then
           Spline.LineTo  cst_end_x2 , cst_end_y2
		  Else
			Polygon.LineTo  cst_end_x2 , cst_end_y2
		  End If
     Next
      If cst_spline_or_poly = 0 Then
      Spline.Create
     Else
      Polygon.create
     End If



With Line
     .Reset
     .Name "end_line"
     .Curve "curve_spiral"
     .X1 cst_end_x1
     .Y1 cst_end_y1
     .X2 cst_end_x2
     .Y2 cst_end_y2
     .Create
End With

With ExtrudeCurve
     .Reset
     .Name cname' Solid.GetNextFreeName
     .Component "Log_Spiral"
     .Material "PEC"
     .Thickness     cname+"_height"
     .Twistangle "0.0"
     .Taperangle "0.0"
     .Curve "curve_spiral"
     .Create
End With

End Sub
Function DialogFunc%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
	Case 1 ' Dialog box initialization
	Case 2 ' Value changing or button pressed
		Select Case Item
		Case "Help"
			StartHelp HelpFileName
			DialogFunc = True
		End Select
	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
Function ShortName (lib_path As String) As String

        Dim lib_dircount As Integer

        lib_dircount  = InStrRev(lib_path, "\")
        ShortName = Mid$(lib_path, lib_dircount+1, 999)

End Function
