' Construct / Coils / Toroidal Coil
' !!! Do not change the line above !!!
' Execute from Macro-Pulldownmenue for a
' proper entry of the history list required for a rebuild
'  macro.510
'-----------------------------------------------------------------------------------------------------------------------------
' Copyright 2001-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'-----------------------------------------------------------------------------------------------------------------------------
' 29-Jun-2021 ube: replace all torrus by torus
' 21-Oct-2015 ube: cancel does not add wcs any more in the history and navigation tree
' 08-Oct-2015 ube: cosmetic change - dialogue title
' 29-Sep-2011 ube,fwe: renamed according to 2nd macro with rectangular core
' 24-Jul-2009 ube: GetMacroPath replaced by GetInstallPath + "\Library\Macros" (previously only first macropath was searched)
' 06-Jun-2007 fhi: checking sweep profile error after rebuild of history; introduced local WCS for construction
' 13-Oct-2005 ube: Included into Online Help
' 24-Feb-2004 jsw: History list updating problem With redundant macro (cancel/help Is clicked) Is fixed
' 01-Aug-2003 jsw: Modefied for parameters sweeping---by jsw/USA
' 10-Oct-2001 fhi: Add Option for Slope either in Deg or Height; correction of Slope-Angle
' 09-Oct-2001 fhi: Original version
'-------------------------------------------------
Option Explicit
Sub Main ()
 Dim cst_torus_r As Double
 Dim cst_torus_phi As Double
 Dim cst_torus_alpha As Double
 Dim cst_torus_i As Integer
 Dim cst_torus_psi As Double, cst_torus_g As Double
 Dim cst_torus_x As Double, cst_torus_y As Double, cst_torus_h As Double
 Dim cst_torus_slope As Double
 Dim cst_torus_N As Integer
 Dim cst_torus_wire_d As Double
 Dim cst_torus_r_torus As Double
 Dim cst_torus_slope_option As Integer

Dim scst_torus_r_torus As String
Dim scst_torus_r As String
Dim scst_torus_phi As String
Dim scst_torus_g As String
Dim scst_torus_slope As String
Dim scst_torus_N As String
Dim scst_torus_wire_d As String
Dim cst_result As Integer


 BeginHide
	Begin Dialog UserDialog 530,294,"3D Toroidal Coil - circular core",.DialogFunc ' %GRID:10,7,1,1
		GroupBox 20,7,260,63,"Torus",.GroupBox1
		Text 30,28,90,21,"Core Radius",.inner
		TextBox 150,21,110,21,.inner_rad
		Text 30,42,110,14,"Outer Radius",.Text1
		TextBox 150,42,110,21,.outer_rad
		GroupBox 20,77,260,63,"Winding",.GroupBox2
		Text 30,98,90,14,"Core Radius",.Text2
		TextBox 150,91,110,21,.wire_inner
		Text 30,112,90,14,"Wire_Radius",.Text3
		TextBox 150,112,110,21,.wire_radius
		OKButton 20,266,100,21
		CancelButton 130,266,90,21
		GroupBox 20,168,500,91,"Turns /Slope",.GroupBox3
		Text 30,189,130,14,"Number of Turns",.Text4
		TextBox 150,182,50,21,.turns
		TextBox 330,182,80,21,.slope
		Text 30,217,110,14,"Angle Increment",.Text6
		TextBox 150,210,50,21,.angle
		Picture 290,7,230,161,GetInstallPath + "\Library\Macros\Construct\Coils\3D Toroidal Coil - circular core.bmp",0,.Picture1
		OptionGroup .option_slope
			OptionButton 310,210,160,21,"Degree",.cst_torus_degree
			OptionButton 310,231,190,14,"Height/Turn (straight Coil)",.cst_torus_height
		Text 280,189,50,14,"Slope ",.Text7
		Text 210,217,50,14,"degree",.Text5
		GroupBox 260,182,10,70,"",.GroupBox4
		Text 280,210,30,14,"in",.Text8
		PushButton 230,266,90,21,"Help",.Help
	End Dialog
 Dim dlg As UserDialog
	
 ' set defaults for dialog
 dlg.inner_rad = "8."
 dlg.outer_rad ="60."
 dlg.wire_inner ="10."
 dlg.wire_radius ="1.5"
 dlg.slope ="5."
 dlg.turns = "7"
 dlg.angle ="30."
 dlg.option_slope = 0
 cst_result=Dialog(dlg)
 assign "cst_result"        '  writes e.g. "cst_result = -1/0/1"     into history list
'Dialog Window
 Do
Loop Until  (dlg.turns <> "")
 
 
scst_torus_r_torus = dlg.inner_rad '8 ' torrus inner radius
scst_torus_r= dlg.wire_inner        ' 10	'wire inner-radius
scst_torus_phi=dlg.angle '30	'delta phi step
scst_torus_g= dlg.outer_rad '60		'torrus Radius
scst_torus_slope = dlg.slope '5 ' slope (Winding distance)
scst_torus_N = dlg.turns '7 	'Number of turns
scst_torus_wire_d = dlg.wire_radius '1.5
cst_torus_slope_option = CInt(dlg.option_slope)

assign "scst_torus_r_torus"
assign "scst_torus_r"
assign "scst_torus_phi"
assign "scst_torus_g"
assign "scst_torus_slope"
assign "scst_torus_N"
assign "scst_torus_wire_d"
assign "cst_torus_slope_option"

EndHide

If (cst_result =0) Then Exit All   ' if cancel/help is clicked, exit all

On Error Resume Next
 'WCS.ActivateWCS "global"
 Solid.Delete "Ferrite:torrus" 
 Solid.Delete "Ferrite:dummy_zyl" 
 Solid.delete "Wire_material:windings"
 Curve.DeleteCurve "torrus_curve" 
 Curve.DeleteCurve "torrus_wire" 
 On Error GoTo 0

'EndHide

If WCS.DoesExist ("WCS_torus") Then
	WCS.reStore "WCS_torus"
Else
	WCS.Store "WCS_torus"
End If

WCS.restore "WCS_torus"

 cst_torus_r_torus = Evaluate(scst_torus_r_torus)
 cst_torus_r =  Evaluate(scst_torus_r)
 cst_torus_phi =  Evaluate(scst_torus_phi)
 cst_torus_g =  Evaluate(scst_torus_g)
 cst_torus_slope =  Evaluate(scst_torus_slope)
 cst_torus_N =  Evaluate(scst_torus_N)
 cst_torus_wire_d =  Evaluate(scst_torus_wire_d)


 ' Begin construction
 'slope 
 If cst_torus_slope_option = 1 Then 'height option
  cst_torus_alpha = Atn(cst_torus_slope/(2*pi*cst_torus_r))*180/pi
 Else 'degree option
  cst_torus_alpha = cst_torus_slope 
 End If
 
 'check for penetration
 If cst_torus_r_torus + cst_torus_wire_d > cst_torus_r Then
  MsgBox "Wire radius +  Torrus Core-radius > Winding Core-Radius!"
  Exit All
 End If 
 'existing items?
 

 Curve.NewCurve "torrus_curve" 
 Curve.NewCurve "torrus_wire" 

 With Material
     .Reset 
     .Name "Ferrite"
     .FrqType "hf" 
     .Type "Normal" 
     .Epsilon "1.0" 
     .Mu "1.0" 
     .Kappa "0.0" 
     .TanD "0.0" 
     .TanDFreq "0.0" 
     .TanDGiven "False" 
     .TanDModel "ConstTanD" 
     .KappaM "0.0" 
     .TanDM "0.0" 
     .TanDMFreq "0.0" 
     .TanDMGiven "False" 
     .DispModelEps "None" 
     .DispModelMu "None" 
     .Rho "0.0" 
     .Colour "0.501961", "0.501961", "0.501961" 
     .Wireframe "False" 
     .Transparency "0" 
     .Create
 End With 

 With Material
     .Reset 
     .Name "Wire_material"
     .FrqType "hf" 
     .Type "Pec" 
     .Rho "0.0" 
     .Colour "1", "0.501961", "0.752941" 
     .Wireframe "False" 
     .Transparency "0" 
     .Create
 End With 

 With Polygon3D 
     .Reset 
     .Name "torrus_3dpolygon" 
     .Curve "torrus_curve" 
     For cst_torus_i = 0 To cst_torus_N*(360/cst_torus_phi)
      'x = r*Cos(phi*i*pi/180)
      cst_torus_y = cst_torus_r*Sin(cst_torus_phi*cst_torus_i*pi/180)
      cst_torus_h = cst_torus_r*cst_torus_phi*cst_torus_i*pi/180*Tan(cst_torus_alpha*pi/180) 
      cst_torus_psi = cst_torus_h/(cst_torus_g-cst_torus_r*Sin(cst_torus_phi*pi/180))
     
      cst_torus_x = cst_torus_g-  _
         (cst_torus_g-cst_torus_r*Cos(cst_torus_phi*cst_torus_i*pi/180))*Cos(cst_torus_psi)	'x-correction
      cst_torus_h = (cst_torus_g-cst_torus_r*  _
          Cos(cst_torus_phi*cst_torus_i*pi/180))*Sin(cst_torus_psi)     	'height- correction
      .Point cst_torus_x, cst_torus_y,cst_torus_h 
     Next cst_torus_i
     .Create 
 End With 

 With Cylinder 
     .Reset 
     .Name "dummy_zyl" 
     .Component "Ferrite" 
     .Material "Ferrite" 
     .OuterRadius cst_torus_r_torus
     .InnerRadius "0" 
     .Axis "z"
     .Zrange "0", "12" 
     .Xcenter "0" 
     .Ycenter "0" 
     .Create 
 End With 
 Pick.PickFaceFromId "Ferrite:dummy_zyl", "1" 
 Pick.AddEdge cst_torus_g, "-1", "0", cst_torus_g, "1", "0" 
 With Rotate 
     .Reset 
     .Name Solid.GetNextFreeName
     .Component "Ferrite" 
     .Material "Ferrite" 
     .Mode "Picks" 
     .Angle "360" 
     .Height "0.0" 
     .RadiusRatio "1.0" 
     .NSteps "0" 
     .Create 
 End With 
 Solid.Delete "Ferrite:dummy_zyl"

 'WCS.ActivateWCS "local"
 WCS.reStore "WCS_torus"

 Pick.PickCurveEndpointFromId "torrus_curve:torrus_3dpolygon", "1"
 WCS.AlignWCSWithSelectedPoint

 'With WCS
     '.SetNormal "0.0", "0.0", "1.0"
     '.SetOrigin cst_torus_r, "0.0", "0.0"
     '.SetUVector "1.0", "0.0", "0.0"
 'End With
 WCS.RotateWCS "u", 90+( cst_torus_alpha) '*180/pi)	'rotate Coordinatesystem

 With Circle
     .Reset 
     .Name "wire" 
     .Curve "torrus_wire" 
     .Radius cst_torus_wire_d 
     .Xcenter "0." 
     .Ycenter "0." 
     .Create
 End With

 With SweepCurve
     .Reset
     .Name Solid.GetNextFreeName
     .Component "Wire_material"
     .Material "Wire_material"
     .Twistangle "0.0"
     .Taperangle "0.0"
     .ProjectProfileToPathAdvanced "False"
     .Path "torrus_curve:torrus_3dpolygon"
     .Curve "torrus_wire:wire"
     .Create
End With

 Curve.DeleteCurve "torrus_curve"
 Curve.DeleteCurve "torrus_wire"
 
 WCS.ActivateWCS "global"
 Pick.ClearAllPicks

End Sub
Function DialogFunc%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
	Case 1 ' Dialog box initialization
	Case 2 ' Value changing or button pressed
		Select Case Item
		Case "Help"
			StartHelp "common_preloadedmacro_Construct_Coils"
			DialogFunc = True
		End Select
	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
