' Construct / Coils / Rectangular Spiral
' !!! Do not change the line above !!!
' macro.509
'-----------------------------------------------------------------------------------------------------------------------------
' Constructs a Rectangular Spiral
'-----------------------------------------------------------------------------------------------------------------------------
' Copyright 2003-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'-----------------------------------------------------------------------------------------------------------------------------
' 28-Jun-2011 ube: removed duplicated points (suggested by Magus team)
' 13-Oct-2005 ube: Included into Online Help
' 24-Feb-2004 jsw: History list updating problem With redundant macro (cancel/help Is clicked) Is fixed
' 01-Aug-2003 jsw: Modefied for parameters sweep
'-----------------------------------------------------------------------------------------------------------------------------
Option Explicit
'-----------------------------------------------------------------------------------------------------------------------------

Sub Main ()

Dim cst_height       As Double
Dim cst_width        As Double
Dim cst_gap          As Double
Dim cst_inner_radius As Double
Dim cst_turns        As Double
Dim scst_height       As String
Dim scst_width        As String
Dim scst_gap          As String
Dim scst_inner_radius As String
Dim scst_turns        As String
Dim cst_segments     As Integer
Dim cst_result       As Integer
Dim cst_radius_ratio As Double
Dim cst_factor       As Double
Dim cst_mean_width   As Double
Dim cst_mean_radius  As Double
Dim cst_oldcount     As Integer
Dim cst_dummyname    As String
Dim cst_index        As Integer
Dim cst_r1           As Double
Dim cst_r2           As Double

BeginHide

        Begin Dialog UserDialog 410,189,"Segmented Spiral settings",.DialogFunc
                GroupBox 10,7,400,147,"",.GroupBox1
                        Text 20,21,100,14,"Height",.HeightLabel
                                TextBox 30,35,150,21,.Height
                        Text 20,63,150,14,"Width",.WidthLabel
                                TextBox 30,77,150,21,.Width
                        Text 20,105,150,14,"Gap",.GapLabel
                                TextBox 30,119,150,21,.Gap
                        Text 220,21,150,14,"Inner Radius",.InnerRadiusLabel
                                TextBox 240,35,150,21,.InnerRadius
                        Text 220,63,150,14,"Number of Turns",.LabelTurns
                                TextBox 240,77,150,21,.Turns
                OKButton 20,161,120,21
                CancelButton 150,161,120,21
                PushButton 280,161,120,21,"Help",.Help
        End Dialog

        Dim dlg As UserDialog
		dlg.Height      = "1"
		dlg.Width       = "3"
		dlg.Gap         = "2"
		dlg.InnerRadius = "10"
		dlg.Turns       = "5"
        cst_result = Dialog(dlg)
        assign "cst_result"        '  writes e.g. "cst_result = -1/0/1"     into history list
        If (cst_result = 0) Then Exit All

        scst_height       = dlg.Height
        scst_width        = dlg.Width
        scst_gap          = dlg.Gap
        scst_inner_radius = dlg.InnerRadius
        scst_turns        = dlg.Turns

        assign "scst_height"       ' writes e.g. "cst_height = 0.1"     into history list
        assign "scst_width"        ' writes e.g. "cst_width = 0.1"      into history list
        assign "scst_gap"          ' writes e.g. "cst_gap = 0.3"        into history list
        assign "scst_inner_radius" ' writes e.g. "cst_inner_radius = 5" into history list
        assign "scst_turns"        ' writes e.g. "cst_turns = 8"        into history list

EndHide

If (cst_result =0) Then Exit All   ' if cancel/help is clicked, exit all
cst_height       = Evaluate(scst_height)
cst_width        = Evaluate( scst_width)
cst_gap          = Evaluate( scst_gap)
cst_inner_radius = Evaluate( scst_inner_radius)
cst_turns        = Evaluate( scst_turns)

With Extrude
		.Reset
		.Name Solid.GetNextFreeName
		.Component "PEC"
		.Material "PEC"
		.Mode "Pointlist"
		.Height cst_height
		.Twist "0.0"
		.Taper "0.0"
		.Origin "0.0", "0.0", "0.0"
		.Uvector "1.0", "0.0", "0.0"
		.Vvector "0.0", "1.0", "0.0"
		.Point cst_inner_radius - cst_gap, cst_inner_radius
		For cst_index = 1 To cst_turns
			cst_r1 = cst_inner_radius + (cst_index - 1) * (cst_width + cst_gap)
			cst_r2 = cst_r1 + cst_width
			.LineTo  cst_r1 - cst_gap, cst_r2
			.LineTo -cst_r2,  cst_r2
			.LineTo -cst_r2, -cst_r2
			.LineTo  cst_r2, -cst_r2
			.LineTo  cst_r2,  cst_r2
		Next
		For cst_index = cst_turns To 1 STEP -1
			cst_r1 = cst_inner_radius + (cst_index - 1) * (cst_width + cst_gap)
			cst_r2 = cst_r1 + cst_width
			.LineTo  cst_r1,  cst_r2
			.LineTo  cst_r1, -cst_r1
			.LineTo -cst_r1, -cst_r1
			.LineTo -cst_r1,  cst_r1
			If cst_index  <> 1 Then
				.LineTo  cst_r1 - cst_width - cst_gap, cst_r1
			End If
		Next
		.Create
End With

End Sub
Function DialogFunc%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
	Case 1 ' Dialog box initialization
	Case 2 ' Value changing or button pressed
		Select Case Item
		Case "Help"
			StartHelp "common_preloadedmacro_Construct_Coils"
			DialogFunc = True
		End Select
	Case 3 ' ComboBox or TextBox Value changed
	Case 4 ' Focus changed
	Case 5 ' Idle
	End Select
End Function
