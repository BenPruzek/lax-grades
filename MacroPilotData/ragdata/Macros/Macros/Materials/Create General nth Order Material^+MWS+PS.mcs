'#Language "WWB-COM"

' Use this macro to create nth order materials. Poles are added sequentially and the material is created once all poles have been entered.

' ================================================================================================
' Copyright 2010-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
' ------------------------------------------------------------------------------------------------
' 22-Feb-2011 fsr,ube: excluded dialogue from history list (Beginhide/endhide)
' 21-Feb-2011 ube: adjusted online help link
' 19-Feb-2010 fsr: fixed a bug for anisotropic materials, changed units to project units
' 18-Feb-2010 fsr: initial version
' ================================================================================================

Option Explicit

Const HelpFileName = "common_preloadedmacro_materials_general_nth_order"

Dim isAnisotropic As Boolean	' Anisotropic material?
Dim nFO%, nSO%					' Number of first and second order poles added
Dim valsFO#(), valsSO#()		' Arrays to hold values for alpha0/beta0/alpha1/beta1
Dim compFO%(), compSO%()		' If anisotropic material, component information needs to be stored as well
Dim historyString$				' This string will be fed to the CST MWS history
Dim iniFileName$				' Name of the ini file to store macro GUI settings
Dim materialName$				' Material Name
Dim i%							' Counter variable

Sub Main

	BeginHide

	Begin Dialog UserDialog 490,287,"Create nth order dispersive material",.DialogFunc ' %GRID:10,7,1,1
		Text 30,21,110,14,"Material name:",.materialNameTT
		TextBox 150,14,150,21,.materialNameT
		Text 30,56,220,14,"epsilon_inf (X/Y/Z if anisotropic):",.epsInfTT
		TextBox 270,49,50,21,.epsInfXT
		TextBox 340,49,50,21,.epsInfYT
		TextBox 410,49,50,21,.epsInfZT
		CheckBox 30,91,100,14,"Anisotropic:",.AnisoChk
		OptionGroup .ComponentOption
			OptionButton 150,91,50,14,"X",.XOptionB
			OptionButton 210,91,40,14,"Y",.YOptionB
			OptionButton 260,91,50,14,"Z",.ZOptionB
		OptionGroup .PoleOption
			OptionButton 30,126,120,14,"1st order pole",.FOOptionB
			OptionButton 150,126,120,14,"2nd order pole",.SOOptionB
		Picture 315,91,150,46,"",0,.Picture1
		TextBox 30,175,100,21,.Alpha0T
		TextBox 140,175,100,21,.Alpha1T
		TextBox 250,175,100,21,.Beta0T
		TextBox 360,175,100,21,.Beta1T
		Text 30,203,230,14,"Coefficients given in project units.",.Text2
		Text 30,231,230,14,"Number of poles added (1st/2nd):",.Text1
		Text 260,231,70,14,"Text2",.NumberT,2
		Text 40,154,80,14,"alpha0",.Text3,2
		Text 260,154,80,14,"beta0",.Text4,2
		Text 150,154,80,14,"alpha1",.Text5,2
		Text 370,154,80,14,"beta1",.Text6,2
		PushButton 80,259,90,21,"Add Pole",.AddPoleB
		PushButton 180,259,90,21,"Create",.CreateB
		CancelButton 280,259,90,21
		PushButton 380,259,90,21,"Help",.HelpB
	End Dialog
	Dim dlg As UserDialog
	Dialog dlg

	EndHide

End Sub

Private Function DialogFunc(DlgItem$, Action%, SuppValue?) As Boolean
	Select Case Action%
	Case 1 ' Dialog box initialization
		InitDialog()
	Case 2 ' Value changing or button pressed
		Select Case DlgItem$
			Case "AnisoChk"
				DlgEnable "ComponentOption",SuppValue?
				isAnisotropic = CBool(SuppValue?)
				DlgEnable "epsInfYT",CBool(SuppValue?)
				DlgEnable "epsInfZT",CBool(SuppValue?)
			Case "PoleOption"
				Select Case DlgValue("PoleOption")
					Case 0
						DlgSetPicture "Picture1", GetInstallPath()+"\Library\Macros\Materials\pole_1storder.BMP",0
						DlgEnable "Alpha1T", False
						DlgEnable "Beta1T", False
					Case 1
						DlgSetPicture "Picture1", GetInstallPath()+"\Library\Macros\Materials\pole_2ndorder.BMP",0
						DlgEnable "Alpha1T", True
						DlgEnable "Beta1T", True
				End Select
			Case "AddPoleB"
				AddPole()
				DialogFunc = True
			Case "CreateB"
				DialogFunc = Not CreateMaterial()
			Case "Cancel"
				Exit All
			Case "HelpB"
				StartHelp HelpFileName
				DialogFunc = True
		End Select
	Case 3 ' TextBox or ComboBox text changed
	Case 4 ' Focus changed
	Case 5 ' Idle
		Rem Wait .1 : DialogFunc = True ' Continue getting idle actions
	Case 6 ' Function key
		Select Case SuppValue?
			Case 1 ' Press F1 for help
				StartHelp HelpFileName
				DialogFunc = True
		End Select
	End Select
End Function

Function InitDialog()
	' Start with no poles, init counter
	nFO = 0
	nSO = 0
	i = 0
	' Initialize arrays
	ReDim compFO(0)
	ReDim compSO(0)
	ReDim valsFO(0)
	ReDim valsSO(0)
	' Ini file name, material name, picture location, and historyString
	iniFileName = GetProjectPath("Root")+"\nthOrderMaterialsMacro.ini"
	historyString = ""
	DlgEnable "AnisoChk", True
	DlgEnable "materialNameT", True
	DlgEnable "epsInfXT", True
	DlgEnable "epsInfYT", True
	DlgEnable "epsInfZT", True
	DlgEnable "CreateB", False
	DlgText "materialNameT", "nthOrderMaterial"
	isAnisotropic = False
	DlgValue "AnisoChk",False
	DlgEnable "XOptionB",False
	DlgEnable "YOptionB",False
	DlgEnable "ZOptionB",False
	DlgEnable "epsInfYT",False
	DlgEnable "epsInfZT",False
	DlgValue "ComponentOption","0"
	DlgValue "PoleOption","0"
	DlgText "Alpha0T", "0.0"
	DlgText "Beta0T", "0.0"
	DlgText "Alpha1T", "0.0"
	DlgText "Beta1T", "0.0"
	DlgSetPicture "Picture1", GetInstallPath()+"\Library\Macros\Materials\pole_1storder.BMP",0
	DlgEnable "Alpha1T", False
	DlgEnable "Beta1T", False
	DlgText "epsInfXT", "1.0"
	DlgText "epsInfYT", "1.0"
	DlgText "epsInfZT", "1.0"
	DlgText "NumberT", Str(nFO)+"/"+Str(nSO)
	DlgEnable "CreateB", False
End Function

Function AddPole()
	' Disable anisotropic checkbox and material name selection cannot be changed anymore
	DlgEnable "AnisoChk", False
	DlgEnable "materialNameT", False
	DlgEnable "epsInfXT", False
	DlgEnable "epsInfYT", False
	DlgEnable "epsInfZT", False
	' Enable Create button as soon as one pole has been entered
	DlgEnable "CreateB", True
	' Store the pole values in arrays
	Select Case DlgValue("PoleOption")
		Case 0
			nFO = nFO + 1
			ReDim Preserve valsFO(2*nFO-1)
			valsFO(2*nFO-1) = Evaluate(DlgText("Alpha0T"))
			valsFO(2*nFO-2) = Evaluate(DlgText("Beta0T"))
			' Always read and store component info, even if not used unless anisotropic
			ReDim Preserve compFO(nFO)
			compFO(nFO-1) = DlgValue("ComponentOption")
		Case 1
			nSO = nSO + 1
			ReDim Preserve valsSO(4*nSO-1)
			valsSO(4*nSO-1) = Evaluate(DlgText("Alpha0T"))
			valsSO(4*nSO-2) = Evaluate(DlgText("Alpha1T"))
			valsSO(4*nSO-3) = Evaluate(DlgText("Beta0T"))
			valsSO(4*nSO-4) = Evaluate(DlgText("Beta1T"))
			' Always read and store component info, even if not used unless anisotropic
			ReDim Preserve compSO(nSO-1)
			compSO(nSO-1) = DlgValue("ComponentOption")
	End Select
	DlgText "NumberT", Str(nFO)+"/"+Str(nSO)
End Function

Function CreateMaterial() As Boolean
	materialName = DlgText("materialNameT")
	historyString = historyString + "With Material" +vbNewLine
	historyString = historyString + "	.Reset" +vbNewLine
	historyString = historyString + "	.Name "+Chr(34)+materialName+Chr(34)+vbNewLine
	historyString = historyString + "	.FrqType "+Chr(34)+"All"+Chr(34)+vbNewLine
	historyString = historyString + "	.Type "+Chr(34)+IIf(isAnisotropic,"Anisotropic","Normal")+Chr(34)+vbNewLine
	historyString = historyString + "	.SetMaterialUnit "+Chr(34)+Units.GetUnit("Frequency")+Chr(34)+","+Chr(34)+Units.GetUnit("Length")+Chr(34)+vbNewLine
	historyString = historyString + "	.Mu "+Chr(34)+"1"+Chr(34)+vbNewLine
	historyString = historyString + "	.DispModelEps "+Chr(34)+"General"+Chr(34)+vbNewLine
	' Insert first order poles
	If isAnisotropic Then
		historyString = historyString + "	.EpsInfinityX " +Chr(34)+Str(Evaluate(DlgText("epsInfXT")))+Chr(34)+vbNewLine
		historyString = historyString + "	.EpsInfinityY " +Chr(34)+Str(Evaluate(DlgText("epsInfYT")))+Chr(34)+vbNewLine
		historyString = historyString + "	.EpsInfinityZ " +Chr(34)+Str(Evaluate(DlgText("epsInfZT")))+Chr(34)+vbNewLine
		For i = 1 To nFO STEP 1
			Select Case compFO(i-1)
				Case 0
					historyString = historyString + "	.AddDispEpsPole1stOrderX " +Chr(34)+Str(valsFO(2*i-1))+Chr(34)+","+Chr(34)+Str(valsFO(2*i-2))+Chr(34)+vbNewLine
				Case 1
					historyString = historyString + "	.AddDispEpsPole1stOrderY " +Chr(34)+Str(valsFO(2*i-1))+Chr(34)+","+Chr(34)+Str(valsFO(2*i-2))+Chr(34)+vbNewLine
				Case 2
					historyString = historyString + "	.AddDispEpsPole1stOrderZ " +Chr(34)+Str(valsFO(2*i-1))+Chr(34)+","+Chr(34)+Str(valsFO(2*i-2))+Chr(34)+vbNewLine
			End Select
		Next i
		' Insert second order poles
		For i = 1 To nSO STEP 1
			Select Case compSO(i-1)
				Case 0
					historyString = historyString + "	.AddDispEpsPole2ndOrderX " +Chr(34)+Str(valsSO(4*i-1))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-2))+Chr(34) _
																		+","+Chr(34)+Str(valsSO(4*i-3))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-4))+Chr(34)+vbNewLine
				Case 1
					historyString = historyString + "	.AddDispEpsPole2ndOrderY " +Chr(34)+Str(valsSO(4*i-1))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-2))+Chr(34) _
																		+","+Chr(34)+Str(valsSO(4*i-3))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-4))+Chr(34)+vbNewLine
				Case 2
					historyString = historyString + "	.AddDispEpsPole2ndOrderZ " +Chr(34)+Str(valsSO(4*i-1))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-2))+Chr(34) _
																		+","+Chr(34)+Str(valsSO(4*i-3))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-4))+Chr(34)+vbNewLine
			End Select
		Next i
	Else
		historyString = historyString + "	.EpsInfinity " +Chr(34)+Str(Evaluate(DlgText("epsInfXT")))+Chr(34)+vbNewLine
		For i = 1 To nFO STEP 1
			historyString = historyString + "	.AddDispEpsPole1stOrder " +Chr(34)+Str(valsFO(2*i-1))+Chr(34)+","+Chr(34)+Str(valsFO(2*i-2))+Chr(34)+vbNewLine
		Next i
		For i = 1 To nSO STEP 1
			historyString = historyString + "	.AddDispEpsPole2ndOrder " +Chr(34)+Str(valsSO(4*i-1))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-2))+Chr(34) _
																+","+Chr(34)+Str(valsSO(4*i-3))+Chr(34)+","+Chr(34)+Str(valsSO(4*i-4))+Chr(34)+vbNewLine
		Next i
	End If
	historyString = historyString + "	.Create" +vbNewLine
	historyString = historyString + "End With" +vbNewLine
	If MsgBox(historyString+vbNewLine+vbNewLine+"Create this material?",vbYesNo,"Material verification")=vbYes Then
		AddToHistory("Create material: "+materialName,historyString)
		CreateMaterial = True
	Else
		InitDialog()
		CreateMaterial = False
	End If
End Function
