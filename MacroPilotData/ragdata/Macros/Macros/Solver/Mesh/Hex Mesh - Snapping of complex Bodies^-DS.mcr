' Limit Snapping for Bodies with many Faces
'-------------------------------------------------------------------------------------------------------
' Copyright 2021-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'-------------------------------------------------------------------------------------------------------
' 23-Jul-2021 ube: dialogue cosmetics, checkin for v2022
' 22-Jul-2021 mba: first version
'-------------------------------------------------------------------------------------------------------

Option Explicit

Function WriteSettingsToHistory(meshType$,useLimit As Boolean,numFaces$)
	Dim useLimitStr As String
	If useLimit Then
		useLimitStr = "1"
	Else
		useLimitStr = "0"
	End If
	Dim historyCommand As String
	Const tab As String = Chr(9)
    Const endLine As String = Chr(13)&Chr(10) 'Windows Newline is CR+LF = 0x0D + 0x0A
 	historyCommand = "'Command generated by macro" & endLine
	historyCommand = historyCommand & "' Limit Snapping for Bodies with many Faces" & endLine
	historyCommand = historyCommand & endLine
    historyCommand = historyCommand & "With MeshSettings" & endLine
	historyCommand = historyCommand & tab & ".SetMeshType " & """" & meshType & """" & endLine
	historyCommand = historyCommand & tab & ".Set ""Version"", 1%" & endLine
	historyCommand = historyCommand & tab & ".Set ""UseMaxNumFacesForSnapping""," & useLimitStr & endLine
	historyCommand = historyCommand & tab & ".Set ""MaxNumFacesForSnapping""," & numFaces & endLine
	historyCommand = historyCommand & "End With" & endLine
	Dim historyEntryTitle As String
	historyEntryTitle ="(*) Hex Mesh Snapping (" & meshType &")"
	Dim couldExecute As Boolean
    couldExecute = AddToHistory(historyEntryTitle, historyCommand)
	If Not couldExecute Then
		ReportWarning("Could not execute generated history block")
		Exit All
    End If
End Function


Sub Main ()

	Begin Dialog UserDialog 430,196,"Limit Snapping for complex Bodies",.DialogFunc ' %GRID:10,7,1,1
		GroupBox 20,7,390,70,"",.GroupBox1
		GroupBox 20,84,390,70,"",.GroupBox2
		CheckBox 35,21,320,21,"Use Snapping Limit for Hexahedral Mesh",.useLimitHexCheckBox
		Text 80,49,150,14,"Number of Faces Hex:",.Text1
		TextBox 270,45,120,21,.numFacesHexTextBox
		Text 80,126,180,14,"Number of Faces HexTLM:",.Text2
		CheckBox 35,98,340,21,"Use Snapping Limit for Hexahedral TLM Mesh",.useLimitHexTLMCheckBox
		TextBox 270,122,120,21,.numFacesHexTLMTextBox
		OKButton 20,165,90,21
		CancelButton 120,165,90,21
	End Dialog
	Dim dlg As UserDialog

	'---------- Initialize dialog   --------------
	With MeshSettings
		.SetMeshType "Hex"
		dlg.useLimitHexCheckBox = .Get("UseMaxNumFacesForSnapping")
		dlg.numFacesHexTextBox = .Get("MaxNumFacesForSnapping")
	End With

	With MeshSettings
		.SetMeshType "HexTLM"
		dlg.useLimitHexTLMCheckBox = .Get("UseMaxNumFacesForSnapping")
		dlg.numFacesHexTLMTextBox = .Get("MaxNumFacesForSnapping")
	End With

	Dim iDialogResponse As Integer
	iDialogResponse = Dialog(dlg)

	If iDialogResponse = 0 Then Exit All

	If iDialogResponse = -1 Then
		'------------ Apply settings ------------------

		WriteSettingsToHistory("Hex",dlg.useLimitHexCheckBox,dlg.numFacesHexTextBox)
		WriteSettingsToHistory("HexTLM",dlg.useLimitHexTLMCheckBox,dlg.numFacesHexTLMTextBox)

		Mesh.Update
	End If

End Sub
Private Function DialogFunc(DlgItem$, Action%, SuppValue?) As Boolean
	Select Case Action%
	Case 1, 2
		DlgEnable "Text1", DlgValue("useLimitHexCheckBox")
		DlgEnable "numFacesHexTextBox", DlgValue("useLimitHexCheckBox")
		'
		DlgEnable "Text2", DlgValue("useLimitHexTLMCheckBox")
		DlgEnable "numFacesHexTLMTextBox", DlgValue("useLimitHexTLMCheckBox")
	Case 3 ' TextBox or ComboBox text changed
	Case 4 ' Focus changed
	Case 5 ' Idle
		Rem Wait .1 : DialogFunc = True ' Continue getting idle actions
	Case 6 ' Function key
	End Select
End Function
