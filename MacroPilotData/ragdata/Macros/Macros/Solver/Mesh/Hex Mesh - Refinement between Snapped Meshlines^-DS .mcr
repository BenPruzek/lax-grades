' Refinement medhod for cell size between snapping planes

' Copyright 2023-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'-------------------------------------------------------------------------------------------------------
' 03-Mar-2023 rsa: first version
'-------------------------------------------------------------------------------------------------------

Option Explicit

Function WriteSettingsToHistory(meshType$,increase As Integer)
	Dim increaseStr As String
	If (increase = 1) Then
		increaseStr = "1"
	Else
		increaseStr = "0"
	End If
	Dim historyCommand As String
	Const tab As String = Chr(9)
    Const endLine As String = Chr(13)&Chr(10) 'Windows Newline is CR+LF = 0x0D + 0x0A
 	historyCommand = "'Command generated by macro" & endLine
	historyCommand = historyCommand & "' Hex Mesh - Refinement Between Snapped Meshlines" & endLine
	historyCommand = historyCommand & endLine
    historyCommand = historyCommand & "With MeshSettings" & endLine
	historyCommand = historyCommand & tab & ".SetMeshType " & """" & meshType & """" & endLine
	historyCommand = historyCommand & tab & ".Set ""Version"", 1%" & endLine
	historyCommand = historyCommand & tab & ".Set ""IncreaseCellSizeOnIncommensurateFitting""," & increaseStr & endLine
	historyCommand = historyCommand & "End With" & endLine
	Dim historyEntryTitle As String
	historyEntryTitle ="(*) Hex Mesh Refinement Between Snapped Meshlines (" & meshType &")"
	Dim couldExecute As Boolean
    couldExecute = AddToHistory(historyEntryTitle, historyCommand)
	If Not couldExecute Then
		ReportWarning("Could not execute generated history block")
		Exit All
    End If
End Function


Sub Main ()

	Dim meshType As String
	With Mesh
        meshType = .GetHexMeshType()
	End With
	Dim title As String
	If meshType = "Hex" Then
		title = "Hex Mesh - Refinement Between Snapped Meshlines"
	ElseIf meshType = "HexTLM" Then
		title = "Hex TLM Mesh - Refinement Between Snapped Meshlines"
	Else
        Exit All
	End If

	Begin Dialog UserDialog 660, 236,title ' %GRID:10,7,1,1
		GroupBox 10, 7, 640, 194,"",.GroupBox4
		Text 30, 28, 560, 69,"Local volume and edge refinement are used to define the cell size around features of interest. This cell size may need to be changed to fit a whole number of cells between snapped meshlines. "+vbCrLf+vbCrLf+"Choose whether the refined cells should be made larger or smaller to fit:",.Text1
		OptionGroup .CellsToFit
			OptionButton 40, 105, 585, 14, "Cells will be made larger than requested to fit between snapped meshlines", .increase
			OptionButton 40, 150, 585, 14, "Cells will be made smaller than requested to fit between snapped meshlines", .decrease
			Text         60, 120, 585, 25, "(This is the default behavior. Cells will be no smaller than the requested refinement)",.Text2
			Text         60, 165, 585, 25, "(Cells will be no larger than the requested refinement. They can be almost twice as small to fit between snapped meshlines, leading to more mesh cells and potentially a smaller timestep)",.Text3
		OKButton      30, 208, 100, 21
		CancelButton 140, 208, 100, 21
	End Dialog
	Dim dlg As UserDialog

	'---------- Initialize dialog   --------------
	Dim schemeName As String
	With MeshSettings
		.SetMeshType meshType
		schemeName = .Get("IncreaseCellSizeOnIncommensurateFitting")
	End With

	Dim schemeSelector As Integer
	If schemeName = "1" Then
		schemeSelector = 0
	Else
		schemeSelector = 1
	End If

	dlg.CellsToFit = schemeSelector

	Dim iDialogResponse As Integer
	iDialogResponse = Dialog(dlg)

	If iDialogResponse = 0 Then Exit All

    If schemeSelector = dlg.CellsToFit Then
        'the setting was not changed
        Exit All
    End If

	If iDialogResponse = -1 Then
		'------------ Apply settings ------------------

		WriteSettingsToHistory(meshType,schemeSelector)
		'WriteSettingsToHistory("Hex",schemeSelector)
		'WriteSettingsToHistory("HexTLM",schemeSelector)

		Mesh.Update
	End If

End Sub


