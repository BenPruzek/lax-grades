' Results / Measure Resonances and Q-values from frq-data
' !!! Do not change the line above !!!
' macro.915
'--------------------------------------------------------------------------------------------
' Copyright 2005-2023 Dassault Systemes Deutschland GmbH
' ================================================================================================
' History of Changes
'--------------------------------------------------------------------------------------------
' 27-May-2020 yco: modifed CalculationQ to simplify the code and improve the accuracy
' 24-Oct-2015 ube: subroutine Start renamed to Start22
' 08-Jun-2011 ube: complex S-Parameters did not work
' 26-Apr-2010 ube: now display proper symbol for AR-filtered results (basenme instead of symbol), axismarkers fixed .Plot added
' 06-Aug-2008 aba: reworked the naming of monitors (old convention did not work with combine results)
' 10-Jan-2008 ube: probes did not work due to changed filenames in 2008 version
' 20-Jun-2006 fhi: extended to dB Results
' 19-Jun-2006 fhi: if missing one of the 3dB Frequ.Points, symmetrical f1, f2 are assumed
' 25-Oct-2005 ube: small adjustment of Help buttons
' 21-Oct-2005 imu: Included into Online Help
' 18-Oct-2005 ube: changed name and position in macros tree slightly
'--------------------------------------------------------------------------------------------

Option Explicit

Const HelpFileName = "common_preloadedmacro_results_measure_resonances_and_q-values_from_frequency_data"

Dim spectrum As Object
Dim cst_freq(10) As Single
Dim calcQdB As Boolean

Sub Main ()

BeginHide
		
		Dim sSelectedItem As String		
		sSelectedItem = GetSelectedTreeItem

		If InStr(sSelectedItem, "1D Results\") <> 1 Then
			MsgBox "Please select a single 1D Result first",vbInformation
			Exit All
		End If
		
        Dim ncurves As Long
        ncurves=Plot1D.GetNumberOfCurves

        Dim filename As String
        Dim basenme As String
        Dim extension As String
        Dim symbol As String
        Dim dircount As Long
        Dim calcQ As Boolean
        Dim tolerance As Double

        symbol = "a1(1)1(1)"
        extension = "sig"
        tolerance = 0.1
        calcQ  = True

        If ncurves > 0 Then
                filename = Plot1D.GetCurveFileName(0)
                basenme  = BaseName(filename)
                dircount = InStr(basenme, "^")
                symbol         = Mid$(basenme, dircount+1)
                extension = ExtName(filename)
                If (extension="sig" And (Left(symbol,4)="ar^d")Or(Left(symbol,1)="d"))   Then
                         calcQdB  = True
                Else
                	calcQdB = False
                End If
                If   (extension="prd") Then
                        tolerance = 5
                        calcQ  = False
                End If
                If (Left(extension,2)="pr" And Left(extension,3)<>"pra" ) Then
                   calcQ  = False
                End If
        End If

	Begin Dialog UserDialog 320,154,"Search Resonances",.DialogFunc1 ' %GRID:10,7,1,1
		Text 20,14,90,14,"Spectrum-ID",.Text1
		TextBox 170,14,140,21,.Spectrum
		Text 20,49,140,14,"Tolerance (absolute)",.Text4
		TextBox 170,49,140,21,.Tolerance
		Text 20,84,60,14,"search",.Text2
		OptionGroup .minmax
			OptionButton 90,77,130,14,"Minima",.OptionButton1
			OptionButton 90,98,130,14,"Maxima",.OptionButton2
		OKButton 10,126,90,21
		CancelButton 110,126,90,21
		PushButton 210,126,90,21,"Help",.Help
	End Dialog
        Dim dlg As UserDialog
                dlg.Spectrum        = basenme
                dlg.Tolerance        = CStr(tolerance)
                dlg.minmax                = 0
        If (Dialog(dlg) >= 0) Then Exit All


    Select Case extension
            Case "sig"
            	If GetFileType(filename) = "complex" Then
            		Dim ccmplx As Object, i As Long
					Set ccmplx = Result1DComplex(dlg.Spectrum)
					
					Begin Dialog UserDialog 340,119,"Complex Result recognized"
						GroupBox 10,14,320,63,"Please select the Result Type to be analyzed:",.GroupBox2
						OptionGroup .GroupReImMagPh
							OptionButton 40,35,50,14,"Re",.OptionButton1
							OptionButton 40,56,40,14,"Im",.OptionButton2
							OptionButton 120,35,50,14,"Mag",.OptionButton3
							OptionButton 120,56,40,14,"Ph",.OptionButton4
							OptionButton 210,35,50,14,"dB",.OptionButton5
						OKButton 20,91,90,21
						CancelButton 120,91,90,21
					End Dialog
					
					Dim dlg23 As UserDialog
					dlg23.GroupReImMagPh = 4
					If (Dialog(dlg23) >= 0) Then Exit All

					Select Case dlg23.GroupReImMagPh
						Case 0
							Set spectrum = ccmplx.Real
						Case 1
							Set spectrum = ccmplx.Imaginary
						Case 2
							Set spectrum = ccmplx.Magnitude
						Case 3
							Set spectrum = ccmplx.Phase
						Case 4
							'calcQ = False  ' disable Q calculation here (ube 8-jun-2011)
							calcQdB = True	' enable calcQdB (yco7 May-27-2020)
							Set spectrum = ccmplx.Magnitude
							With spectrum
								For i = 0 To .GetN-1
									If .GetY(i)>0 Then
										.SetXYDouble(i,.GetX(i),20.0*Log(.GetY(i))/Log(10))
									Else
										.SetXYDouble(i,.GetX(i),-120.0)
									End If
								Next
							End With
					End Select

            	Else
					Set spectrum = Result1D(dlg.Spectrum)
            	End If
            Case "prs"
                        Set spectrum = Result1D (dlg.Spectrum+".prs")
            Case "pra"
                        Set spectrum = Result1D (dlg.Spectrum+".pra")
            Case "prd"
                        Set spectrum = Result1D (dlg.Spectrum+".prd")
            Case "prp"
                        Set spectrum = Result1D (dlg.Spectrum+".prp")
        End Select

'spectrum.Save "^harry.sig"
'spectrum.AddToTree "1D Results\Harry"

        Dim nres As Long
        Dim v1,v2,v3,v4 As Double
        Dim limit As Double
        Dim MinMax As String

        If (dlg.minmax=0) Then
                MinMax = "min"
        Else
                MinMax = "max"
        End If

        limit        = RealVal(dlg.Tolerance)
        If (MinMax = "min") Then
                nres = spectrum.GetFirstMinimum(limit)
        Else
                nres = spectrum.GetFirstMaximum(limit)
        End If

        While nres=-1
				Begin Dialog UserDialog 310,84,"No resonances found",.DialogFunc1 ' %GRID:10,7,1,1
					Text 20,14,150,14,"Decrease tolerance",.Text2
					TextBox 180,14,90,21,.Tolerance
					OKButton 10,49,90,21
					CancelButton 110,49,90,21
					PushButton 210,49,90,21,"Help",.Help
				End Dialog

                Dim dlg1 As UserDialog
                dlg1.Tolerance        = CStr(limit)
                If (Dialog(dlg1) >= 0) Then Exit All
                limit        = RealVal(dlg1.Tolerance)
                If (MinMax = "min") Then
                        nres = spectrum.GetFirstMinimum(limit)
                Else
                        nres = spectrum.GetFirstMaximum(limit)
                End If
        Wend

EndHide

        Const NMax = 10
        Dim cst_frequency(NMax) As Single
        Dim cst_amplitude(NMax) As Single
        Dim cst_qfactor(NMax) As Single
        Dim cst_string(NMax) As String
        Dim cst_checkbox(NMax) As Boolean
        Dim NFound As Long
        Dim cstmon_e As Long
        Dim cstmon_h As Long
        Dim cstmon_p As Long
        Dim cstmon_f As Long

BeginHide

        NFound = 0
        Do
                If NFound > NMax-1 Then
                        MsgBox "Arrays filled (Maximum Number of resonances="+CStr(NMax)+")"
                        Exit Do
                End If
                NFound = NFound+1
                cst_frequency(NFound) = spectrum.GetX(nres)
                cst_freq(NFound) = cst_frequency(NFound)
                cst_amplitude(NFound) = spectrum.GetY(nres)
                cst_qfactor(NFound) = 0
				'MsgBox CStr(calcQ)
                If calcQ Then
                        cst_qfactor(NFound) = CalculateQ(MinMax,nres)
                End If

                cst_string(NFound) = "          " + CStr(cst_frequency(NFound)) + _
                                                          "          " + CStr(cst_amplitude(NFound)) + _
                                                          "          " + CStr(cst_qfactor(NFound))

                If (MinMax = "min") Then
                        nres = spectrum.GetNextMinimum(limit)
                Else
                        nres = spectrum.GetNextMaximum(limit)
                End If

        Loop Until nres = -1

        Dim dlg_hoehe As Long
        dlg_hoehe = 343-(NMax-NFound)*21

        Begin Dialog UserDialog 490,dlg_hoehe,"Summary of resonances",.DialogFunc
                GroupBox 20,14,450,70,"Monitors ( to be defined )",.GroupBox1
                PushButton 330,28,110,21,"Define monitors",.PushButton1
                PushButton 350,56,80,21,"End",.PushButton2
                CheckBox 40,35,90,14,"E-Field",.CheckElectric
                CheckBox 40,56,140,14,"H-Field / Current",.CheckMagnetic
                CheckBox 200,35,100,14,"Power flow",.CheckPower
                CheckBox 200,56,90,14,"Farfield",.CheckFarfield
                Text 30,98,430,14,"Monitor        Frequency         Amplitude         Qfactor",.Text1
                Text 30,112,180,14,"yes/no",.Text2
                CheckBox 40,133,420,14,cst_string(1),.CheckBox1
                CheckBox 40,154,420,14,cst_string(2),.CheckBox2
                CheckBox 40,175,420,14,cst_string(3),.CheckBox3
                CheckBox 40,196,420,14,cst_string(4),.CheckBox4
                CheckBox 40,217,420,14,cst_string(5),.CheckBox5
                CheckBox 40,238,420,14,cst_string(6),.CheckBox6
                CheckBox 40,259,420,14,cst_string(7),.CheckBox7
                CheckBox 40,280,420,14,cst_string(8),.CheckBox8
                CheckBox 40,301,420,14,cst_string(9),.CheckBox9
                CheckBox 40,322,420,14,cst_string(10),.CheckBox10
		End Dialog
		
        Dim dlg2 As UserDialog
        If (Dialog(dlg2)=2) Then Exit All

        If dlg2.CheckBox1  Then cst_checkbox(1)=True
        If dlg2.CheckBox2  Then cst_checkbox(2)=True
        If dlg2.CheckBox3  Then cst_checkbox(3)=True
        If dlg2.CheckBox4  Then cst_checkbox(4)=True
        If dlg2.CheckBox5  Then cst_checkbox(5)=True
        If dlg2.CheckBox6  Then cst_checkbox(6)=True
        If dlg2.CheckBox7  Then cst_checkbox(7)=True
        If dlg2.CheckBox8  Then cst_checkbox(8)=True
        If dlg2.CheckBox9  Then cst_checkbox(9)=True
        If dlg2.CheckBox10 Then cst_checkbox(10)=True

        cstmon_e = dlg2.CheckElectric
        cstmon_h = dlg2.CheckMagnetic
        cstmon_p = dlg2.CheckPower
        cstmon_f = dlg2.CheckFarfield

        Dim ii As Long
        For ii=1 To NFound
                assign "cst_checkbox("  + CStr(ii) + ")"
                assign "cst_frequency(" + CStr(ii) + ")"
        Next ii
        assign "NFound"

        assign "cstmon_e"
        assign "cstmon_h"
        assign "cstmon_p"
        assign "cstmon_f"

        ScreenUpdating False

EndHide

        Dim iimode As Long
        For iimode=1 To NFound
                If cst_checkbox(iimode) Then
                        With Monitor
                            .Reset
                            .Dimension "Volume"
                            .Frequency Replace(CStr(cst_frequency(iimode)),",", ".")
                                If cstmon_e Then
                                    .Name "e-field (f="+Replace(CStr(cst_frequency(iimode)),",", ".")+")"
                                    .FieldType "Efield"
                                    .Create
                                End If
                                If cstmon_h Then
                                    .Name "h-field (f="+Replace(CStr(cst_frequency(iimode)),",", ".")+")"
                                    .FieldType "Hfield"
                                    .Create
                                End If
                                If cstmon_p Then
                                    .Name "power (f="+Replace(CStr(cst_frequency(iimode)),",", ".")+")"
                                    .FieldType "Powerflow"
                                    .Create
                                End If
                                If cstmon_f Then
                                    .Name "farfield (f="+Replace(CStr(cst_frequency(iimode)),",", ".")+")"
                                    .FieldType "Farfield"
                                    .Create
                                End If
                        End With
                End If
        Next

BeginHide

        ScreenUpdating True
        MsgBox "Definition of Monitors is finished."

EndHide

End Sub
'-----------------------------------------------------------------------------------------------------------------------------

Function DialogFunc%(Item As String, Action As Integer, Value As Integer)

        Select Case Action
                Case 1 ' Dialog box initialization
                Case 2 ' Value changing or button pressed
                        Select Case Item
							Case "Help"
								StartHelp HelpFileName
								DialogFunc = True
							Case "CheckBox1"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(1))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox2"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(2))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox3"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(3))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox4"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(4))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox5"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(5))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox6"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(6))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox7"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(7))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox8"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(8))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox9"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(9))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
							Case "CheckBox10"
									Plot1D.XMarker True
									Plot1D.XMarkerPos(cst_freq(10))
									Plot1D.Plot
									ScreenUpdating True
									DialogFunc%= True
                        End Select
                Case 3 ' ComboBox or TextBox Value changed
                Case 4 ' Focus changed
                Case 5 ' Idle
        End Select
End Function
'--------------------------------------------------------------------------------------------
Function DialogFunc1%(Item As String, Action As Integer, Value As Integer)
	Select Case Action
		Case 1 ' Dialog box initialization
		Case 2 ' Value changing or button pressed
			Select Case Item
			Case "Help"
				StartHelp HelpFileName
				DialogFunc1 = True
			End Select
		Case 3 ' ComboBox or TextBox Value changed
		Case 4 ' Focus changed
		Case 5 ' Idle
	End Select
End Function

'--------------------------------------------------------------------------------------------
' modified the left and right frequency by linear regression (yco7 May-27-2020)
Function CalculateQ(MinMax As String, nres As Long) As Single

        Dim vres As Double, v1 As Double, v2 As Double, v3dB As Double, vpre As Double
        Dim fres As Double, f1 As Double, f2 As Double
        Dim nfrq As Long, ii As Long
        Dim noLeft As Boolean, noRight As Boolean

        nfrq = spectrum.GetN
        vres = spectrum.GetY(nres)
        fres = spectrum.GetX(nres)

        If MinMax="min" Then
                v3dB = Sqr(0.5*(vres*vres+1))	' middle of 1 and valley in power (yco7 May-27-2020)
                If calcQdB Then
                  v3dB = vres + 3				' 3 dB upper the vally in dB curve (yco7 May-27-2020)
                End If
        Else
                v3dB = vres / Sqr(2)			' half of peak in power (yco7 May-27-2020)
                If calcQdB Then
                  v3dB = vres - 3				' 3 dB lower the peak in dB curve (yco7 May-27-2020)
                End If
        End If

        noLeft = True
        vpre = vres
        For ii = nres-1 To 0 STEP -1
                v1 = spectrum.GetY(ii)
                If MinMax="min" And v1 < vpre Or MinMax="max" And v1 > vpre Then Exit For
				If MinMax="min" And v1 > v3dB Or MinMax="max" And v1 < v3dB Then
						noLeft = False
                        f1 = CalculateX(v3dB, spectrum.GetX(ii+1), spectrum.GetX(ii), vpre, v1)
                        Exit For
				End If
                vpre = v1
        Next ii

        noRight = True
        vpre = vres
        For ii = nres+1 To nfrq-1
                v2 = spectrum.GetY(ii)
                If MinMax="min" And v2 < vpre Or MinMax="max" And v2 > vpre Then Exit For
				If MinMax="min" And v2 > v3dB Or MinMax="max" And v2 < v3dB Then
						noRight = False
                        f2 = CalculateX(v3dB, spectrum.GetX(ii-1), spectrum.GetX(ii), vpre, v2)
                        Exit For
				End If
                vpre = v2
        Next ii

        If noLeft Then
				MsgBox "Lower Frequency point not found; used symmetrical point "
                CalculateQ = fres / (2*Abs(fres - f2))
        ElseIf noRight Then
				MsgBox "Upper Frequency point not found; used symmetrical point "
				CalculateQ = fres / (2*Abs(fres - f1))
		Else
				CalculateQ = fres / Abs(f2 - f1)
        End If

End Function

'--------------------------------------------------------------------------------------------
' linear regression and calculate x from y (yco7 May-27-2020)
Function CalculateX(y As Double, x1 As Double, x2 As Double, y1 As Double, y2 As Double) As Double

		Dim a As Double, b As Double

		b = (y2 - y1)/(x2 - x1)		' the slope b will not be zero because this operates at a peak or velley (yco7 May-27-2020)
		a = y1 - b * x1

		CalculateX = (y - a)/b

End Function

'-----------------------------------------------------------------------------------------------------------------------------
' Useful VBA procedures and functions
'-----------------------------------------------------------------------------------------------------------------------------
'
' To avoid name collision with parameters in CST MicroWaveStudio,
' all variable names in this library have the prefix "lib_"
'
Public lib_FindFileRoot As String
Public lib_FindFilePattern As String
Public lib_FindFileRecursive As Boolean
Public lib_FindFileLast As String
'-----------------------------------------------------------------------------------------------------------------------------

Sub Start22 (lib_filename As String)

        On Error GoTo Win95
        WINNT:
                Shell "cmd /c " + Quote(lib_filename)
                Exit Sub
        Win95:
                Shell "start " + Quote(lib_filename)
                Exit Sub

End Sub

'-----------------------------------------------------------------------------------------------------------------------------

Function FindFirstFile (lib_rootdir As String, lib_pattern As String, lib_recursive As Boolean) As String

        If (Right$(lib_rootdir, 1) = "\") Then lib_rootdir = Left$(lib_rootdir, Len(lib_rootdir) - 1)
        If (lib_pattern = "")             Then lib_pattern = "*.*"

        lib_FindFileRoot      = lib_rootdir
        lib_FindFilePattern   = lib_pattern
        lib_FindFileRecursive = lib_recursive
        lib_FindFileLast      = Dir$(lib_rootdir + "\*.*", vbDirectory)
        FindFirstFile     = FindNextFile()

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function FindNextFile () As String

        Dim lib_subdir As String, lib_currentdir, lib_dummy As String, lib_short As String, lib_filename As String
        Dim lib_fileattribute As Integer

        lib_subdir = DirName(lib_FindFileLast)
        lib_dummy  = Dir$()

        While (Not lib_dummy Like lib_FindFilePattern)

'                If MsgBox(IIf(lib_subdir <> "", lib_subdir + "\" + lib_dummy, lib_dummy), vbQuestion + vbYesNo, "Continue") <> vbYes Then Exit All
                lib_currentdir = lib_FindFileRoot + IIf(lib_subdir <> "", "\" + lib_subdir, "")

                On Error Resume Next
                        lib_fileattribute = 0
                        lib_fileattribute = GetAttr(lib_currentdir + "\" + lib_dummy)
                On Error GoTo 0

                If (lib_dummy = "") Then
                        If (lib_subdir = "") Then Exit While
                        lib_short      = ShortName(lib_subdir)
                        lib_subdir     = DirName(lib_subdir)
                        lib_currentdir = IIf(lib_subdir <> "", lib_FindFileRoot + "\" + lib_subdir, lib_FindFileRoot)
                        lib_dummy      = Dir$(lib_currentdir + "\*.*", vbDirectory)
                        While (lib_dummy <> lib_short)
                                lib_dummy = Dir$()
                        Wend
                        lib_dummy = Dir$()
                ElseIf (lib_dummy = ".") Or (lib_dummy = "..") Then
                        lib_dummy = Dir$()
                ElseIf (lib_fileattribute = vbDirectory) And lib_FindFileRecursive Then
                        lib_subdir     = IIf(lib_subdir <> "", lib_subdir + "\" + lib_dummy, lib_dummy)
                        lib_currentdir = IIf(lib_subdir <> "", lib_FindFileRoot + "\" + lib_subdir, lib_FindFileRoot)
                        lib_dummy      = Dir$(lib_currentdir + "\*.*", vbDirectory)
                Else
                        lib_dummy = Dir$()
                End If
        Wend
        lib_filename     = IIf(lib_dummy <> "", IIf(lib_subdir <> "", lib_subdir + "\", "") + lib_dummy, "")
        lib_FindFileLast = lib_filename

        FindNextFile     = lib_filename

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function DriveName (lib_path As String) As String

        DriveName  = Left$(lib_path, 1)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function DirName (lib_path As String) As String

        Dim lib_dircount As Integer

        lib_dircount = InStrRev(lib_path, "\")
        DirName  = Left$(lib_path, IIf(lib_dircount > 1, lib_dircount - 1, 0))

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function ShortName (lib_path As String) As String

        Dim lib_dircount As Integer

        lib_dircount  = InStrRev(lib_path, "\")
        ShortName = Mid$(lib_path, lib_dircount+1, 999)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function BaseName (lib_path As String) As String

        Dim lib_dircount As Integer, lib_extcount As Integer, lib_filename As String

        lib_dircount = InStrRev(lib_path, "\")
        lib_filename = Mid$(lib_path, lib_dircount+1)
        lib_extcount = InStrRev(lib_filename, ".")
        BaseName = Left$(lib_filename, IIf(lib_extcount > 0, lib_extcount-1, 999))

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function ExtName (lib_filename As String) As String

        Dim lib_extcount As Integer

        lib_extcount = InStrRev(lib_filename, ".")
        ExtName = IIf(lib_extcount > 0, Mid$(lib_filename, lib_extcount+1), "")

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function FullPath(lib_filename As String, lib_directory As String) As String

        FullPath = IIf(Mid$(lib_filename,2,1) = ":", lib_filename, lib_directory + "\" + lib_filename)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function ShortPath(lib_filename As String, lib_directory As String) As String

        If (LCase$(DirName(lib_filename)) = LCase$(lib_directory)) Then
                ShortPath = ShortName(lib_filename)
        Else
                ShortPath = lib_filename
        End If

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function Quote (lib_Text As String) As String

        Quote = Chr$(34) + lib_Text + Chr$(34)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function GetString(lib_application As String, lib_section As String, lib_key As String, lib_default As String) As String

        Dim lib_dummy As String

        lib_dummy = GetSetting(lib_application, lib_section, lib_key)
        GetString = IIf(lib_dummy <> "", lib_dummy, lib_default)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Function GetInteger(lib_application As String, lib_section As String, lib_key As String, lib_default As Integer) As Integer

        Dim lib_dummy As String

        lib_dummy  = GetSetting(lib_application, lib_section, lib_key)
        GetInteger = IIf(lib_dummy <> "", Val(lib_dummy), lib_default)

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Sub SaveString(lib_application As String, lib_section As String, lib_key As String, lib_value As String)

        SaveSetting lib_application, lib_section, lib_key, lib_value

End Sub

'-----------------------------------------------------------------------------------------------------------------------------

Sub SaveInteger(lib_application As String, lib_section As String, lib_key As String, lib_value As Integer)

        SaveSetting lib_application, lib_section, lib_key, CStr(lib_value)

End Sub

'-----------------------------------------------------------------------------------------------------------------------------

Function RealVal(lib_Text As Variant) As Double

        If (CDbl("0.5") > 1) Then
                RealVal = CDbl(Replace(lib_Text, ".", ","))
        Else
		On Error Resume Next
                	RealVal = CDbl(lib_Text)
		On Error GoTo 0
        End If

End Function

'-----------------------------------------------------------------------------------------------------------------------------

Sub CheckApplication (lib_appname As String, lib_filename As String, lib_downloadsource As String)

        Dim lib_newline As String, lib_appdir As String, lib_hotlink As String
        lib_newline = Chr$(10) + Chr$(13)
        lib_appdir  = DirName(lib_filename)
        lib_hotlink = lib_appdir + lib_appname + ".url"

        If (Dir$(lib_filename, vbNormal) = "") Then
                If (MsgBox( _
                                lib_appname + " must be installed in" + lib_newline + lib_newline + _
                                lib_appdir                            + lib_newline + lib_newline + _
                                "Download " + lib_appname + " from"   + lib_newline + lib_newline + _
                                lib_downloadsource, _
                                vbOkCancel + vbQuestion, _
                                lib_appname + " is missing" _
                        ) = vbOK) Then
                        If (Dir$(lib_appdir, vbDirectory) = "") Then
                                MkDir lib_appdir
                        End If
                        Open lib_hotlink For Output As #1
                                Print #1, "[DEFAULT]"
                                Print #1, "BASEURL=" + lib_downloadsource
                                Print #1, "[InternetShortcut]"
                                Print #1, "URL=" + lib_downloadsource
                        Close #1
                        Shell "explorer " + lib_downloadsource
                End If
                Exit All
        End If

End Sub

'-----------------------------------------------------------------------------------------------------------------------------

Sub ShowHelp (lib_name As String)

        Dim lib_filename As String, lib_extension() As Variant, lib_index As Integer

        lib_extension = Array("htm", "html", "txt", "rtf", "doc", "hlp")

        lib_index = 0
        Do
                    lib_filename = GetMacroPath + "\" + Dir$(GetMacroPath + "\*" + lib_name + "." + lib_extension(lib_index))
                    lib_index = lib_index + 1
        Loop Until (lib_filename <> "" Or lib_extension(lib_index) = "")

        Start22 lib_filename

End Sub

'-----------------------------------------------------------------------------------------------------------------------------
